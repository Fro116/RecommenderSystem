{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "8569f4e0-f062-400e-b480-d6c237b18735",
   "metadata": {},
   "source": [
    "# Reranking\n",
    "* The reranking stage happens after the ranking stage\n",
    "* The ranking stage learns the user's preference relation over items\n",
    "  * Does the user prefer item A over item B?\n",
    "* The reranking stage learns the user's preference relation over recommendation lists\n",
    "  * Does the user prefer recommendation list A over recommendation list B? \n",
    "* The reranking stage is structured as a quadratic programming problem\n",
    "  * Given a set of items, find the subset that is optimized for relevance, diversity, and novelty"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5a9e7df6-973b-46bf-849d-f891366c8f18",
   "metadata": {},
   "outputs": [],
   "source": [
    "# TODO import instead of using\n",
    "using Ipopt \n",
    "using JuMP"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "22fc480b-bf7f-49e4-9455-dd5ebb8c4b54",
   "metadata": {},
   "outputs": [],
   "source": [
    "@memoize function get_similarity_metric(weights)\n",
    "    sum(\n",
    "        [\n",
    "            read_params(\"all/WatchSimilarity\")[\"S\"],\n",
    "            read_params(\"all/GenreSimilarity\")[\"S\"],\n",
    "            read_params(\"all/TagSimilarity\")[\"S\"],\n",
    "        ] .* weights ./ sum(weights),\n",
    "    )\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "690e4578-e0ab-468e-b3e2-eff877f9962a",
   "metadata": {},
   "outputs": [],
   "source": [
    "function solve_quadratic_program(\n",
    "    list_size,\n",
    "    similarity_metric,\n",
    "    relevance_scores,\n",
    "    attributes,\n",
    "    relative_constraints,\n",
    ")\n",
    "    # solves the quadratic program:\n",
    "    # minimize x' * similarity_metric * x - relevance_scores * x \n",
    "    # with the constraints:\n",
    "    # sum(x) = list_size\n",
    "    # x \\in {0, 1} are binary variables    \n",
    "    # attributes[i]' * x <= constraints[i] for all i\n",
    "\n",
    "    # scale problem by list_size\n",
    "    N = size(similarity_metric)[1]\n",
    "    similarity_metric ./= list_size^2\n",
    "    relevance_scores ./= list_size\n",
    "    constraints = [ceil(x * list_size) for x in relative_constraints]\n",
    "\n",
    "    # make the initial optimization problem convex\n",
    "    # this will not change the solution to the minimization problem\n",
    "    posdef_penalty = 0\n",
    "    while !LinearAlgebra.isposdef(similarity_metric)\n",
    "        similarity_metric -= posdef_penalty * LinearAlgebra.I(N)\n",
    "        posdef_penalty = max(1, posdef_penalty * 2)\n",
    "        similarity_metric += posdef_penalty * LinearAlgebra.I(N)\n",
    "    end\n",
    "\n",
    "    # solve the mixed-integer quadratic problem by\n",
    "    # teratively solving the QP with an increasing penalty \n",
    "    # for non-binary solutions\n",
    "    nonbinary_penalty = 0\n",
    "    model = nothing\n",
    "    warm_start = zeros(N)\n",
    "    epsilon = 0.01\n",
    "    while sum(warm_start .> epsilon) != list_size\n",
    "        # model\n",
    "        model = Model(\n",
    "            optimizer_with_attributes(Ipopt.Optimizer, \"print_level\" => 0, \"sb\" => \"yes\"),\n",
    "        )\n",
    "        @variable(model, 0 <= x[1:N] <= 1)\n",
    "        JuMP.set_start_value.(x, warm_start)\n",
    "\n",
    "        # constraints\n",
    "        @constraint(model, sum(x) == list_size)\n",
    "        for i = 1:length(constraints)\n",
    "            @constraint(model, attributes[i]' * x <= constraints[i])\n",
    "        end\n",
    "\n",
    "        #objective\n",
    "        @expression(model, relevance, relevance_scores' * x)\n",
    "        @expression(model, similarity, x' * similarity_metric * x)\n",
    "        @expression(model, penalty, nonbinary_penalty * x' * (1 .- x))\n",
    "        @objective(model, Min, similarity - relevance + penalty)\n",
    "\n",
    "        # solve\n",
    "        optimize!(model)\n",
    "        y = value.(x)\n",
    "        nonbinary_penalty = max(1, nonbinary_penalty * 2)\n",
    "        if nonbinary_penalty > 2^10\n",
    "            @info \"could not solve miqp $list_size. dropping constraint\"\n",
    "            @assert length(constraints) > 0\n",
    "            skip = (list_size % length(constraints)) + 1\n",
    "            return solve_quadratic_program(\n",
    "                list_size,\n",
    "                similarity_metric,\n",
    "                relevance_scores,\n",
    "                [attributes[i] for i in 1:length(attributes) if i != skip],\n",
    "                [relative_constraints[i] for i in 1:length(constraints) if i != skip],\n",
    "            )\n",
    "        end\n",
    "        warm_start = value.(x)\n",
    "    end\n",
    "\n",
    "    collect(1:length(warm_start))[warm_start.>epsilon]\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d91ad250-8a28-4dd0-8c6a-b87edd5b4c20",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_reranking_order(\n",
    "    similarity_metric,\n",
    "    list_size,\n",
    "    relevance_scores,\n",
    "    attributes,\n",
    "    constraints,\n",
    ")\n",
    "    # get a subset of the list_size best recs\n",
    "    candidates = solve_quadratic_program(\n",
    "        list_size,\n",
    "        similarity_metric,\n",
    "        relevance_scores,\n",
    "        attributes,\n",
    "        constraints,\n",
    "    )\n",
    "\n",
    "    # rerank the recs such that the top N items \n",
    "    # are a optimal subset for any 1 <= N <= list_size\n",
    "    order = []\n",
    "    while length(candidates) > 0\n",
    "        new_candidates = candidates[solve_quadratic_program(\n",
    "            length(candidates) - 1,\n",
    "            similarity_metric[candidates, candidates],\n",
    "            relevance_scores[candidates],\n",
    "            [x[candidates] for x in attributes],\n",
    "            constraints,\n",
    "        )]\n",
    "        worst_candidate = [x for x in candidates if x âˆ‰ new_candidates][1]\n",
    "        candidates = [x for x in candidates if x != worst_candidate]\n",
    "        pushfirst!(order, worst_candidate)\n",
    "    end\n",
    "    order\n",
    "end\n",
    "\n",
    "function rerank(\n",
    "    df,\n",
    "    list_size;\n",
    "    similarity_penalty,\n",
    "    long_runner_constraint,\n",
    "    related_constraint,\n",
    "    similarity_weights,\n",
    ")\n",
    "    S = similarity_penalty * get_similarity_metric(similarity_weights)[df.uid.+1, df.uid.+1]\n",
    "    order = get_reranking_order(\n",
    "        S,\n",
    "        list_size,\n",
    "        exp.(df.score),\n",
    "        [df.long_runner, df.related],\n",
    "        [long_runner_constraint, related_constraint],\n",
    "    )\n",
    "    df[order, :]\n",
    "end;"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.3",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
