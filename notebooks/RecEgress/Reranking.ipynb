{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "8569f4e0-f062-400e-b480-d6c237b18735",
   "metadata": {},
   "source": [
    "# Reranking\n",
    "* The reranking stage happens after the ranking stage\n",
    "* The ranking stage learns the user's preference relation over items\n",
    "  * Does the user prefer item A over item B?\n",
    "* The reranking stage learns the user's preference relation over recommendation lists\n",
    "  * Does the user prefer recommendation list A over recommendation list B? \n",
    "* The reranking stage is modelled as a binary quadratic programming problem\n",
    "  * Given a set of items, find the optimal subset by solving a QP\n",
    "  * We get a ranked list by iteratively taking subsets"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "06f98c6d-e2fa-4d81-a904-8d8205fa93f5",
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, SCIP"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0827186f-b428-459a-adf3-d1a1e1dc0aa7",
   "metadata": {},
   "outputs": [],
   "source": [
    "@memoize function get_similarity_metric(medium::String, weights = nothing)\n",
    "    alphas = [\"$medium/all/WatchSimilarity\", \"$medium/all/GenreSimilarity\"]\n",
    "    if medium == \"anime\"\n",
    "        push!(alphas, \"$medium/all/TagSimilarity\")\n",
    "    end\n",
    "    if isnothing(weights)\n",
    "        weights = ones(length(alphas))\n",
    "    end\n",
    "    weights = convert.(Float16, weights ./ sum(weights))\n",
    "    \n",
    "    S = zeros(Float16, num_items(medium), num_items(medium))\n",
    "    for (a, w) in Iterators.zip(alphas, weights)\n",
    "        GC.gc()\n",
    "        S += read_params(a)[\"S\"] * w\n",
    "    end\n",
    "    return S\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ab9335f1-abba-468f-bca5-0cb2e0417297",
   "metadata": {},
   "outputs": [],
   "source": [
    "function solve_linear_program(list_size, relevance_scores, constraint_spec)\n",
    "    # solves the linear program:\n",
    "    # maximize relevance_scores * x \n",
    "    # with the constraints:\n",
    "    # sum(x) = list_size\n",
    "    # x \\in {0, 1} are binary variables    \n",
    "    # attributes[i]' * x <= constraints[i] for all i\n",
    "\n",
    "    if list_size == 0\n",
    "        return []\n",
    "    end\n",
    "\n",
    "    # scale problem by list_size\n",
    "    N = length(relevance_scores)\n",
    "    relevance_scores = relevance_scores ./ list_size\n",
    "    attributes = constraint_spec[1]\n",
    "    constraints = [f(list_size) for f in constraint_spec[2]]\n",
    "\n",
    "    # setup optimization problem in JuMP\n",
    "    model = Model(SCIP.Optimizer)\n",
    "    set_attribute(model, \"display/verblevel\", 0)\n",
    "    @variable(model, 0 <= x[1:N] <= 1, Bin)\n",
    "    @constraint(model, sum(x) == list_size)\n",
    "    for i = 1:length(constraints)\n",
    "        @constraint(model, attributes[i]' * x <= constraints[i])\n",
    "    end\n",
    "    @objective(model, Max, relevance_scores' * x)\n",
    "\n",
    "    # solve\n",
    "    optimize!(model)\n",
    "    try\n",
    "        solution = value.(x)\n",
    "        return sortperm(solution)[end-list_size+1:end]\n",
    "    catch\n",
    "        # no feasible points. Randomly drop a consraint and retry\n",
    "        @assert length(constraints) > 0\n",
    "        skip = (list_size % length(constraints)) + 1\n",
    "        return solve_linear_program(\n",
    "            list_size,\n",
    "            relevance_scores,\n",
    "            (\n",
    "                [v for (i, v) in Iterators.enumerate(constraint_spec[1]) if i != skip],\n",
    "                [v for (i, v) in Iterators.enumerate(constraint_spec[2]) if i != skip],\n",
    "            ),\n",
    "        )\n",
    "    end\n",
    "    @assert false\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "36da77a8-a342-414c-9236-1178052688fc",
   "metadata": {},
   "outputs": [],
   "source": [
    "function solve_quadratic_program(\n",
    "    list_size,\n",
    "    similarity_metric,\n",
    "    relevance_scores,\n",
    "    constraint_spec,\n",
    ")\n",
    "    # solves the quadratic program:\n",
    "    # minimize x' * similarity_metric * x - relevance_scores * x \n",
    "    # with the constraints:\n",
    "    # sum(x) = list_size\n",
    "    # x \\in {0, 1} are binary variables    \n",
    "    # attributes[i]' * x <= constraints[i] for all i\n",
    "\n",
    "    if list_size == 0\n",
    "        return []\n",
    "    end\n",
    "\n",
    "    # scale problem by list_size\n",
    "    N = size(similarity_metric)[1]\n",
    "    similarity_metric = similarity_metric ./ list_size^2\n",
    "    relevance_scores = relevance_scores ./ list_size\n",
    "    attributes = constraint_spec[1]\n",
    "    constraints = [f(list_size) for f in constraint_spec[2]]\n",
    "\n",
    "    # solve the mixed-integer quadratic problem by doing a brute \n",
    "    # force search. \n",
    "    @assert N - 1 == list_size # limit N for performance\n",
    "    scores = zeros(N)\n",
    "    for i = 1:N\n",
    "        x = ones(N)\n",
    "        x[i] = 0\n",
    "        feasible = true\n",
    "        for (a, c) in Iterators.zip(attributes, constraints)\n",
    "            if a' * x > c\n",
    "                feasible = false\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        if feasible\n",
    "            scores[i] = x' * similarity_metric * x - relevance_scores' * x\n",
    "        else\n",
    "            scores[i] = Inf\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # if no feasible points are found, drop a random constraint and retry\n",
    "    if all(scores .== Inf)\n",
    "        @assert length(constraints) > 0\n",
    "        skip = (list_size % length(constraints)) + 1\n",
    "        return solve_quadratic_program(\n",
    "            list_size,\n",
    "            similarity_metric,\n",
    "            relevance_scores,\n",
    "            (\n",
    "                [v for (i, v) in Iterators.enumerate(constraint_spec[1]) if i != skip],\n",
    "                [v for (i, v) in Iterators.enumerate(constraint_spec[2]) if i != skip],\n",
    "            ),\n",
    "        )\n",
    "    end\n",
    "\n",
    "    chosen = argmin(scores)\n",
    "    [x for x in 1:N if x != chosen]\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "da61d993-71f0-4c38-bd21-b0f91340d88b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_seasonal_constraints(df, limitfrac)\n",
    "    attributes = []\n",
    "    constraints = []\n",
    "    start_seasons = copy(df.start_season)\n",
    "    start_seasons[ismissing.(start_seasons)] .= \"\"\n",
    "    seasons = Set(start_seasons)\n",
    "    for season in Set(start_seasons)\n",
    "        if season == \"\"\n",
    "            continue\n",
    "        end\n",
    "        attrib = convert.(Float32, start_seasons .== season)\n",
    "        if sum(attrib) > 1\n",
    "            push!(attributes, attrib)\n",
    "            push!(constraints, N -> ceil(limitfrac * N))\n",
    "        end\n",
    "    end\n",
    "    attributes, constraints\n",
    "end\n",
    "\n",
    "function get_intrarelated_constraints(df, medium, limitfrac)\n",
    "    S = read_params(\"$medium/all/RelatedSeries/similarity_matrix\")[\"S\"]\n",
    "    attributes = []\n",
    "    constraints = []\n",
    "    for uid in df.uid\n",
    "        x = S[:, uid][df.uid]\n",
    "        if sum(x) > 1\n",
    "            if any(all(x .== y) for y in attributes)\n",
    "                continue\n",
    "            end\n",
    "            push!(attributes, collect(x))\n",
    "            push!(constraints, N -> 1 + limitfrac * N)\n",
    "        end\n",
    "    end\n",
    "    attributes, constraints\n",
    "end\n",
    "\n",
    "function get_interrelated_constraints(df, limitfrac)\n",
    "    # num items related to the same series\n",
    "    attributes = [convert.(Float32, df.is_related .!= 0)]\n",
    "    constraints = [N -> ceil(limitfrac * N)]\n",
    "    attributes, constraints\n",
    "end\n",
    "\n",
    "function get_crossrelated_constraints(df, limitfrac)\n",
    "    # num items related to any watched series \n",
    "    attributes = [convert.(Float32, df.is_cross_related .!= 0)]\n",
    "    constraints = [N -> ceil(limitfrac * N)]\n",
    "    attributes, constraints\n",
    "end\n",
    "\n",
    "function get_ptw_constraints(df, limitfrac)\n",
    "    # num items related to any watched series     \n",
    "    attributes = [convert.(Float32, df.ptw .!= 0)]\n",
    "    constraints = [N -> ceil(limitfrac * N)]\n",
    "    attributes, constraints\n",
    "end\n",
    "\n",
    "function merge_constraints(args...)\n",
    "    Tuple(reduce(vcat, [x[t] for x in args]) for t = 1:2)\n",
    "end\n",
    "\n",
    "function get_constraint_spec(\n",
    "    df,\n",
    "    medium,\n",
    "    spec,\n",
    ")\n",
    "    constraints = [\n",
    "        get_intrarelated_constraints(df, medium, spec[:intrarelated]),\n",
    "        get_interrelated_constraints(df, spec[:interrelated]),\n",
    "        get_crossrelated_constraints(df, spec[:crossrelated]),\n",
    "        get_ptw_constraints(df, spec[:ptw]),\n",
    "    ]\n",
    "    if medium == \"anime\"\n",
    "        push!(constraints, get_seasonal_constraints(df, spec[:seasonal]))\n",
    "    end\n",
    "    merge_constraints(constraints...)\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "96c9bfaa-ddf0-416c-976e-4b4e53dc4af8",
   "metadata": {},
   "outputs": [],
   "source": [
    "function rerank(df, medium::String, list_size, similarity_spec, constraint_spec)\n",
    "    S =\n",
    "        similarity_spec[:penalty] *\n",
    "        get_similarity_metric(medium, similarity_spec[:weights])[df.uid, df.uid]\n",
    "    constraint_spec = get_constraint_spec(df, medium, constraint_spec)\n",
    "    candidates = solve_linear_program(list_size, df.score, constraint_spec)\n",
    "\n",
    "    order = []\n",
    "    while length(candidates) > 0\n",
    "        new_S = S[candidates, candidates]\n",
    "        new_score = df.score[candidates]\n",
    "        new_constraint_spec =\n",
    "            ([x[candidates] for x in constraint_spec[1]], constraint_spec[2])\n",
    "        new_order = solve_quadratic_program(\n",
    "            length(candidates) - 1,\n",
    "            new_S,\n",
    "            new_score,\n",
    "            new_constraint_spec,\n",
    "        )\n",
    "        worst_candidate = candidates[findfirst(x -> x ∉ new_order, 1:length(candidates))]\n",
    "        candidates = candidates[new_order]\n",
    "        pushfirst!(order, worst_candidate)\n",
    "    end\n",
    "\n",
    "    df[order, :]\n",
    "end\n",
    "rerank(args...) = x -> rerank(x, args...);"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
