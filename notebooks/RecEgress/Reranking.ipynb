{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "8569f4e0-f062-400e-b480-d6c237b18735",
   "metadata": {},
   "source": [
    "# Reranking\n",
    "* The reranking stage happens after the ranking stage\n",
    "* The ranking stage learns the user's preference relation over items\n",
    "  * Does the user prefer item A over item B?\n",
    "* The reranking stage learns the user's preference relation over recommendation lists\n",
    "  * Does the user prefer recommendation list A over recommendation list B? \n",
    "* The reranking stage is modelled as a binary quadratic programming problem\n",
    "  * Given a set of items, find the optimal subset by solving a QP\n",
    "  * We get a ranked list by iteratively taking subsets"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "06f98c6d-e2fa-4d81-a904-8d8205fa93f5",
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "import Ipopt"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0827186f-b428-459a-adf3-d1a1e1dc0aa7",
   "metadata": {},
   "outputs": [],
   "source": [
    "@memoize function get_similarity_metric(medium::String, weights = nothing)\n",
    "    alphas = [\"$medium/all/WatchSimilarity\", \"$medium/all/GenreSimilarity\"]\n",
    "    if medium == \"anime\"\n",
    "        push!(alphas, \"$medium/all/TagSimilarity\")\n",
    "    end\n",
    "    if isnothing(weights)\n",
    "        weights = ones(Float32, length(alphas))\n",
    "    end\n",
    "    convert.(Float32, weights ./ sum(weights))\n",
    "    sum([read_params(x)[\"S\"] for x in alphas] .* weights)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "96f4c236-428c-47e7-ac38-ce173f226268",
   "metadata": {},
   "outputs": [],
   "source": [
    "function solve_quadratic_program(\n",
    "    list_size,\n",
    "    similarity_metric,\n",
    "    relevance_scores,\n",
    "    constraint_spec,\n",
    ")\n",
    "    # solves the quadratic program:\n",
    "    # minimize x' * similarity_metric * x - relevance_scores * x \n",
    "    # with the constraints:\n",
    "    # sum(x) = list_size\n",
    "    # x \\in {0, 1} are binary variables    \n",
    "    # attributes[i]' * x <= constraints[i] for all i\n",
    "\n",
    "    # scale problem by list_size\n",
    "    N = size(similarity_metric)[1]\n",
    "    similarity_metric = similarity_metric ./ list_size^2\n",
    "    relevance_scores = relevance_scores ./ list_size\n",
    "    attributes = constraint_spec[1]\n",
    "    constraints = [f(list_size) for f in constraint_spec[2]]\n",
    "\n",
    "    # make the initial optimization problem convex\n",
    "    # this will not change the solution to the minimization problem\n",
    "    posdef_penalty = 0\n",
    "    while !LinearAlgebra.isposdef(similarity_metric)\n",
    "        similarity_metric -= posdef_penalty * LinearAlgebra.I(N)\n",
    "        posdef_penalty = max(1, posdef_penalty * 2)\n",
    "        similarity_metric += posdef_penalty * LinearAlgebra.I(N)\n",
    "        if posdef_penalty > 2^16\n",
    "            @assert false \"similarity matrix is not positive definite\"\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # solve the mixed-integer quadratic problem by\n",
    "    # iteratively solving the QP with an increasing penalty \n",
    "    # for non-binary solutions\n",
    "    nonbinary_penalty = 0\n",
    "    model = nothing\n",
    "    solution = zeros(N)\n",
    "    epsilon = 0.01\n",
    "    while sum(solution .> epsilon) != list_size\n",
    "        # model\n",
    "        model = Model(\n",
    "            optimizer_with_attributes(Ipopt.Optimizer, \"print_level\" => 0, \"sb\" => \"yes\"),\n",
    "        )\n",
    "        @variable(model, 0 <= x[1:N] <= 1)\n",
    "\n",
    "        # constraints\n",
    "        @constraint(model, sum(x) == list_size)\n",
    "        for i = 1:length(constraints)\n",
    "            @constraint(model, attributes[i]' * x <= constraints[i])\n",
    "        end\n",
    "\n",
    "        #objective\n",
    "        @expression(model, relevance, relevance_scores' * x)\n",
    "        @expression(model, similarity, x' * similarity_metric * x)\n",
    "        @expression(model, penalty, nonbinary_penalty * x' * (1 .- x))\n",
    "        @objective(model, Min, similarity - relevance + penalty)\n",
    "\n",
    "        # solve\n",
    "        optimize!(model)\n",
    "        y = value.(x)\n",
    "        nonbinary_penalty = max(1, nonbinary_penalty * 2)\n",
    "        if nonbinary_penalty > 2^10\n",
    "            @info \"could not solve miqp $list_size. dropping constraint\"\n",
    "            @assert length(constraints) > 0\n",
    "            skip = (list_size % length(constraints)) + 1\n",
    "            return solve_quadratic_program(\n",
    "                list_size,\n",
    "                similarity_metric,\n",
    "                relevance_scores,\n",
    "                (\n",
    "                    [v for (i, v) in Iterators.enumerate(constraint_spec[1]) if i != skip],\n",
    "                    [v for (i, v) in Iterators.enumerate(constraint_spec[2]) if i != skip],\n",
    "                ),\n",
    "            )\n",
    "        end\n",
    "        solution = value.(x)\n",
    "    end\n",
    "\n",
    "    collect(1:length(solution))[solution.>epsilon]\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "da61d993-71f0-4c38-bd21-b0f91340d88b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_intrarelated_constraints(df, medium, limitfrac)\n",
    "    S = read_params(\"$medium/all/RelatedSeries/similarity_matrix\")[\"S\"]\n",
    "    attributes = []\n",
    "    constraints = []\n",
    "    for uid in df.uid\n",
    "        x = S[:, uid][df.uid]\n",
    "        if sum(x) > 1\n",
    "            if any(all(x .== y) for y in attributes)\n",
    "                continue\n",
    "            end\n",
    "            push!(attributes, collect(x))\n",
    "            push!(constraints, N -> 1 + limitfrac * N)\n",
    "        end\n",
    "    end\n",
    "    attributes, constraints\n",
    "end\n",
    "\n",
    "function get_interrelated_constraints(df, limitfrac)\n",
    "    attributes = [convert.(Float32, df.is_related .!= 0)]\n",
    "    constraints = [N -> ceil(limitfrac * N)]\n",
    "    attributes, constraints\n",
    "end\n",
    "function get_crossrelated_constraints(df, limitfrac)\n",
    "    attributes = [convert.(Float32, df.is_cross_related .!= 0)]\n",
    "    constraints = [N -> ceil(limitfrac * N)]\n",
    "    attributes, constraints\n",
    "end\n",
    "\n",
    "function get_ptw_constraints(df, limitfrac)\n",
    "    attributes = [convert.(Float32, df.ptw .!= 0)]\n",
    "    constraints = [N -> ceil(limitfrac * N)]\n",
    "    attributes, constraints\n",
    "end\n",
    "\n",
    "function merge_constraints(args...)\n",
    "    Tuple(reduce(vcat, [x[t] for x in args]) for t = 1:2)\n",
    "end\n",
    "\n",
    "function get_constraint_spec(df, medium)\n",
    "    merge_constraints(\n",
    "        get_intrarelated_constraints(df, medium, 0.01),\n",
    "        get_interrelated_constraints(df, 0.1),\n",
    "        get_crossrelated_constraints(df, 0.1),\n",
    "        get_ptw_constraints(df, 0.1),\n",
    "    )\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1e628529-7da8-4768-be2d-8e35f3d6e5dd",
   "metadata": {},
   "outputs": [],
   "source": [
    "function rerank(df, medium::String, list_size, similarity_spec)\n",
    "    GC.gc()\n",
    "    S =\n",
    "        similarity_spec[2] *\n",
    "        get_similarity_metric(medium, similarity_spec[1])[df.uid.+1, df.uid.+1]\n",
    "    constraint_spec = get_constraint_spec(df, medium)\n",
    "    candidates = solve_quadratic_program(list_size, S, df.score, constraint_spec)\n",
    "\n",
    "    # TODO block reranking\n",
    "    # first select items in groups of 5\n",
    "    # then rerank is 5 group\n",
    "    order = []\n",
    "    while length(candidates) > 0\n",
    "        new_S = S[candidates, candidates]\n",
    "        new_score = df.score[candidates]\n",
    "        new_constraint_spec =\n",
    "            ([x[candidates] for x in constraint_spec[1]], constraint_spec[2])\n",
    "        new_order = solve_quadratic_program(\n",
    "            length(candidates) - 1,\n",
    "            new_S,\n",
    "            new_score,\n",
    "            new_constraint_spec,\n",
    "        )\n",
    "        worst_candidate = candidates[findfirst(x -> x âˆ‰ new_order, 1:length(candidates))]\n",
    "        candidates = candidates[new_order]\n",
    "        pushfirst!(order, worst_candidate)\n",
    "    end\n",
    "    \n",
    "    score = sum(df.score[order]) / length(order) - sum(S[order, order]) / length(order)^2\n",
    "    df[order, :]\n",
    "end\n",
    "rerank(args...) = x -> rerank(x, args...);"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.0-rc2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
