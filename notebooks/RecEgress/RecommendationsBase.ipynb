{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "240e43fd-abc8-4d44-a3d6-85293d0dd756",
   "metadata": {},
   "outputs": [],
   "source": [
    "const source_name = \"Recommendations\";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ba187524-034d-40f6-9e40-0ab5123e36bb",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "import NBInclude: @nbinclude\n",
    "@nbinclude(\"../InferenceAlphas/Alpha.ipynb\");\n",
    "using DataFrames\n",
    "import CSV\n",
    "import Statistics: mean, quantile, cor\n",
    "import Random\n",
    "@nbinclude(\"Reranking.ipynb\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2dc4c5b5-a759-463c-b163-e47fd461a554",
   "metadata": {},
   "outputs": [],
   "source": [
    "ENV[\"DATAFRAMES_COLUMNS\"] = 300;\n",
    "ENV[\"DATAFRAMES_ROWS\"] = 300;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "82a21cba-2259-45a9-a0d6-2a36d57ebb6b",
   "metadata": {},
   "outputs": [],
   "source": [
    "if source == \"MAL\"\n",
    "    sourcename = \"MyAnimeList\"\n",
    "elseif source == \"AniList\"\n",
    "    sourcename = \"AniList\"\n",
    "elseif source == \"Kitsu\"\n",
    "    sourcename = \"Kitsu\"\n",
    "else\n",
    "    @assert false\n",
    "end\n",
    "@info \"Displaying anime and manga recommendations for $username from $sourcename\""
   ]
  },
  {
   "cell_type": "markdown",
   "id": "16859e20-8b33-4118-8ab5-4e4972807300",
   "metadata": {},
   "source": [
    "# Merge recommendations with item metadata"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f86579c5-06f8-4f20-a49b-1d7a6e337f01",
   "metadata": {},
   "outputs": [],
   "source": [
    "function to_hyperlink(title, url)\n",
    "    \"<a href=\\\"$url\\\">$title</a>\"\n",
    "end\n",
    "\n",
    "function get_hyperlink(title, links, source)\n",
    "    if source == \"MAL\"\n",
    "        search = \"myanimelist\"\n",
    "    elseif source == \"AniList\"\n",
    "        search = \"anilist\"\n",
    "    elseif source == \"XML\"\n",
    "        search = \"MAL\"\n",
    "    else\n",
    "        @assert false\n",
    "    end\n",
    "\n",
    "    # try to return the preferred source\n",
    "    parsed_links = eval(Meta.parse(replace(links, \"'\" => \"\\\"\")))\n",
    "    for link in parsed_links\n",
    "        if occursin(search, link)\n",
    "            return to_hyperlink(title, link)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # default to mal\n",
    "    search = \"myanimelist\"\n",
    "    parsed_links = eval(Meta.parse(replace(links, \"'\" => \"\\\"\")))\n",
    "    for link in parsed_links\n",
    "        if occursin(search, link)\n",
    "            return to_hyperlink(title, link)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return title\n",
    "end\n",
    "\n",
    "function parse_time(x)\n",
    "    minutes = Int(round(x / 60))\n",
    "    hours = Int(floor(minutes / 60))\n",
    "    minutes = minutes % 60\n",
    "    function time_string(x, suffix)\n",
    "        if x == 0\n",
    "            return \"\"\n",
    "        else\n",
    "            return \"$(x)$(suffix) \"\n",
    "        end\n",
    "    end\n",
    "    time_string(hours, \"h\") * time_string(minutes, \"m\")\n",
    "end\n",
    "\n",
    "function get_media(medium::String)\n",
    "    df = DataFrame(\n",
    "        CSV.File(\n",
    "            get_data_path(\"processed_data/$medium.csv\"),\n",
    "            ntasks = 1;\n",
    "            stringtype = String,\n",
    "        ),\n",
    "    )\n",
    "    df.title = get_hyperlink.(df.title, df.links, source)\n",
    "    df\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "12e5d877-d062-478a-b72f-cbd31ac61e1b",
   "metadata": {},
   "outputs": [],
   "source": [
    "@memoize function get_media_to_uid(medium)\n",
    "    media = get_media(medium)\n",
    "    media_to_uid = DataFrame(CSV.File(\"../../data/processed_data/$(medium)_to_uid.csv\"))\n",
    "    media_to_uid = innerjoin(media_to_uid, media, on = \"$(medium)id\" => \"$(medium)_id\")\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7398a34d-13c6-4b7d-9838-99c40fe83ed9",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_rec_df(task, medium)\n",
    "    media_to_uid = get_media_to_uid(medium)\n",
    "\n",
    "    get_alpha(x, task = task) =\n",
    "        read_recommendee_alpha(\"$medium/$task/$x\", \"all\", medium).rating\n",
    "    rating_df = DataFrame(\n",
    "        \"uid\" => 0:num_items(medium)-1,\n",
    "        \"explicit\" => get_alpha(\"LinearExplicit\"),\n",
    "        \"implicit\" => get_alpha(\"LinearImplicit\"),\n",
    "        \"implicit_seq\" => get_alpha(\"Transformer/v1/implicit\"),\n",
    "        \"implicit_bag\" => get_alpha(\"NeuralImplicitUniversalUntuned\"),\n",
    "        \"explicit_seq\" => get_alpha(\"Transformer/v1/explicit\"),\n",
    "        \"explicit_bag\" => get_alpha(\"NeuralExplicitUniversalUntuned\"),\n",
    "        \"explicit_baseline\" => get_alpha(\"ExplicitUserItemBiases\"),\n",
    "        \"num_dependencies\" => get_alpha(\"Dependencies\", \"all\"),\n",
    "        \"is_sequel\" => get_alpha(\"SequelSeries\", \"all\"),\n",
    "        \"is_direct_sequel\" => get_alpha(\"DirectSequelSeries\", \"all\"),\n",
    "        \"is_related\" => get_alpha(\"RelatedSeries\", \"all\"),\n",
    "        \"is_recap\" => get_alpha(\"RecapSeries\", \"all\"),\n",
    "        \"is_cross_recap\" => get_alpha(\"CrossRecapSeries\", \"all\"),\n",
    "        \"is_cross_related\" => get_alpha(\"CrossRelatedSeries\", \"all\"),\n",
    "        \"score1\" => get_alpha(\"MLE.Ensemble.1\"),\n",
    "        \"score2\" => get_alpha(\"MLE.Ensemble.2\"),\n",
    "        \"score3\" => get_alpha(\"MLE.Ensemble.3\"),\n",
    "        \"score4\" => get_alpha(\"MLE.Ensemble.4\"),\n",
    "        \"score5\" => get_alpha(\"MLE.Ensemble.5\"),\n",
    "        \"score6\" => get_alpha(\"MLE.Ensemble.6\"),\n",
    "    )\n",
    "\n",
    "    if DEBUG\n",
    "        @info \"Debug Info\"\n",
    "        for (content, name) in\n",
    "            [(\"explicit\", \"rated\"), (\"implicit\", \"watched\"), (\"ptw\", \"planned to watch\")]\n",
    "            n = length(get_recommendee_split(content, medium).item)\n",
    "            @info \"$n items $name\"\n",
    "        end\n",
    "        df = get_recommendee_split(\"explicit\", medium)\n",
    "        if length(df.item) > 0\n",
    "            rss = sum((df.rating - rating_df.explicit[df.item]) .^ 2)\n",
    "            tss = sum((df.rating .- mean(df.rating)) .^ 2)\n",
    "            @info \"RMSE: $(sqrt(rss / length(df.rating)))\"\n",
    "            @info \"R2: $(1 - rss / tss)\"\n",
    "            @info \"Correlation: $(cor(df.rating, rating_df.explicit[df.item]))\"\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # don't recommend shows that the user has already seen before\n",
    "    rating_df[:, \"seen\"] .= false\n",
    "    rating_df.seen[get_recommendee_split(\"implicit\", medium).item] .= true\n",
    "    rating_df[:, \"ptw\"] .= false\n",
    "    rating_df.ptw[get_recommendee_split(\"ptw\", medium).item] .= true\n",
    "    # TODO something special for ptw series\n",
    "\n",
    "    rec_df = innerjoin(media_to_uid, rating_df, on = \"uid\")\n",
    "    rec_df[:, \"uid\"] .+= 1\n",
    "    if medium == \"anime\"\n",
    "        series_length = [\"num_episodes\"]\n",
    "    elseif medium == \"manga\"\n",
    "        series_length = [\"num_volumes\", \"num_chapters\"]\n",
    "    end\n",
    "    keepcols = vcat(\n",
    "        [\"$(medium)id\", \"uid\", \"title\", \"medium\"],\n",
    "        series_length,\n",
    "        [\"genres\", \"tags\", \"start_date\"],\n",
    "    )\n",
    "    if medium == \"anime\"\n",
    "        push!(keepcols, \"start_season\")\n",
    "    end\n",
    "    for x in names(rating_df)\n",
    "        if x ∉ keepcols\n",
    "            push!(keepcols, x)\n",
    "        end\n",
    "    end\n",
    "    rec_df = rec_df[:, keepcols]\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6c2f0d81-6437-4f9e-9d67-804212c24d68",
   "metadata": {},
   "source": [
    "# Display options"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bc4fa33a-6a6c-4ee1-ac51-b45b306ea1e8",
   "metadata": {},
   "outputs": [],
   "source": [
    "display_filter(f) = (df; fn = identity) -> filter(fn ∘ f, df)\n",
    "inv(f) = (x...) -> f(x...; fn = !)\n",
    "seen = display_filter(x -> x.seen)\n",
    "ptw = display_filter(x -> x.ptw)\n",
    "related = display_filter(x -> x.related != 0)\n",
    "crossrecap = display_filter(x -> x.is_cross_recap != 0 && x.is_direct_sequel == 0)\n",
    "recap = display_filter(x -> x.is_recap != 0)\n",
    "tv = display_filter(x -> x.medium in [\"tv\", \"ona\"])\n",
    "dependent = display_filter(x -> x.num_dependencies > 0 && x.is_direct_sequel == 0)\n",
    "released(medium) = display_filter(\n",
    "    x ->\n",
    "        !ismissing(x.start_date) &&\n",
    "            (x.start_date < timestamp_to_date(1, medium)) &&\n",
    "            released_item(medium, x),\n",
    ")\n",
    "head(n) = x -> first(x, n);\n",
    "top(n, field) = x -> first(sort(x, field, rev = true), n)\n",
    "top(n) = top(n, :score)\n",
    "max_episodes(n) = display_filter(x -> x.num_episodes <= n)\n",
    "startdate(year, month = 1) =\n",
    "    display_filter(x -> x.start_date >= Dates.DateTime(year, month))\n",
    "search(title, col = :title) = display_filter(x -> occursin(lowercase(title), lowercase(x[col])));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "0fda2a41-8cc4-41fc-a2c9-a04b59fce968",
   "metadata": {},
   "outputs": [],
   "source": [
    "function score(df, x)\n",
    "    df[:, :score] = df[:, x]\n",
    "    df\n",
    "end\n",
    "score(x) = df -> score(df, x);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "81483049-d27d-4870-96a8-95c324879e27",
   "metadata": {},
   "outputs": [],
   "source": [
    "function released_item(medium::String, x)\n",
    "    if medium == \"anime\"\n",
    "        return (ismissing(x.num_episodes) || (x.num_episodes > 0)) ||\n",
    "               ismissing(x.start_date) && !ismissing(x.num_episodes) && (x.num_episodes > 0)\n",
    "    elseif medium == \"manga\"\n",
    "        return (ismissing(x.num_chapters) || (x.num_chapters > 0)) ||\n",
    "               ismissing(x.start_date) && !ismissing(x.num_chapters) && (x.num_chapters > 0)\n",
    "    else\n",
    "        @assert false\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "39308d35-d4cb-437d-a5c4-c4542b0b268f",
   "metadata": {},
   "outputs": [],
   "source": [
    "function display(df::DataFrame, medium::String)\n",
    "    df = select(\n",
    "        df,\n",
    "        :title,\n",
    "        :score,\n",
    "        Not([:title, :score, :is_recap, :seen, Symbol(\"$(medium)id\"), :tags]), # , :uid\n",
    "    )\n",
    "    if !DEBUG\n",
    "        if medium == \"anime\"\n",
    "            series_length = [:num_episodes]\n",
    "        elseif medium == \"manga\"\n",
    "            series_length = [:num_volumes, :num_chapters]\n",
    "        end\n",
    "        df = select(df, vcat([:title, :medium], series_length, [:start_date, :genres]))      \n",
    "    end\n",
    "    headers = titlecase.(replace.(names(df), \"_\" => \" \"))\n",
    "    headers = replace(\n",
    "        headers,\n",
    "        \"Medium\" => \"Type\",\n",
    "        \"Explicit\" => \"Rating\",\n",
    "        \"Implicit\" => \"Watch Probability\",\n",
    "        \"Average Episode Duration\" => \"Episode Length\",\n",
    "        \"Num Episodes\" => \"Episodes\",\n",
    "        \"Num Volumes\" => \"Volumes\",\n",
    "        \"Num Chapters\" => \"Chapters\",\n",
    "    )\n",
    "    Base.show(\n",
    "        stdout,\n",
    "        MIME(\"text/html\"),\n",
    "        df;\n",
    "        allow_html_in_cells = true,\n",
    "        header = headers,\n",
    "        nosubheader = true,\n",
    "        row_number_column_title = \"Rank\",\n",
    "        top_left_str = \"\",\n",
    "    )\n",
    "end\n",
    "display(medium::String) = x -> display(x, medium);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3bb84b54-5981-460b-a0a4-d99ec99f8b37",
   "metadata": {},
   "outputs": [],
   "source": [
    "recommend(\n",
    "    medium;\n",
    "    task = \"temporal_causal\",\n",
    "    suffix = \".2\",\n",
    "    M = 500,\n",
    "    N = 20,\n",
    "    similarity_spec = (nothing, 0.1),\n",
    ") =\n",
    "    get_rec_df(task, medium, suffix) |>\n",
    "    inv(seen) |>\n",
    "    inv(recap) |>\n",
    "    inv(dependent) |>\n",
    "    inv(crossrecap) |>\n",
    "    released(medium) |>\n",
    "    top(M) |>\n",
    "    rerank(medium, N, similarity_spec) |>\n",
    "    display(medium);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4aec4878-970d-44ff-a14f-dc89c539b83f",
   "metadata": {},
   "outputs": [],
   "source": [
    "recommend(\n",
    "    medium;\n",
    "    task = \"temporal_causal\",\n",
    "    relevance = \"score3\",\n",
    "    M = 500,\n",
    "    N = 50,\n",
    "    similarity_spec = (weights = nothing, penalty = 1),\n",
    "    constraint_spec = (\n",
    "        intrarelated = 0,\n",
    "        interrelated = 1,\n",
    "        crossrelated = 0.1,\n",
    "        ptw = 0.1,\n",
    "        seasonal = 0.25,\n",
    "    ),\n",
    ") =\n",
    "    get_rec_df(task, medium) |>\n",
    "    inv(seen) |>\n",
    "    inv(recap) |>\n",
    "    inv(dependent) |>\n",
    "    inv(crossrecap) |>\n",
    "    released(medium) |>\n",
    "    score(relevance) |>\n",
    "    top(M) |>\n",
    "    rerank(medium, N, similarity_spec, constraint_spec) |>\n",
    "    display(medium);"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.0-rc2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
