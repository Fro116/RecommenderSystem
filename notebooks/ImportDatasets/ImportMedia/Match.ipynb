{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "0062296d-d2af-4eeb-886e-d8812cb627b4",
   "metadata": {},
   "source": [
    "# Match items across sources"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0dfd6fc8-fea3-4c65-ac5f-d7dfdef203a0",
   "metadata": {},
   "outputs": [],
   "source": [
    "import JupyterFormatter\n",
    "JupyterFormatter.enable_autoformat();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "113de4bf-6ec6-420c-8b41-f6756a0a33f7",
   "metadata": {},
   "outputs": [],
   "source": [
    "import CSV\n",
    "import DataFrames\n",
    "import Glob\n",
    "import JSON\n",
    "import Memoize: @memoize\n",
    "import ProgressMeter: @showprogress\n",
    "import Random\n",
    "import SHA\n",
    "import StatsBase"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ffb4f871-ba24-4da9-8aa3-4e96e0e0b308",
   "metadata": {},
   "outputs": [],
   "source": [
    "itemtype = Tuple{String,String};"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c3b6bce4-a7a7-499f-8f5b-d1d7fce33375",
   "metadata": {},
   "outputs": [],
   "source": [
    "function read_csv(x::String; kw...)\n",
    "    CSV.read(x, DataFrames.DataFrame; types = String, missingstring = nothing, kw...)\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a24be1a3-a106-4f25-b8ee-1bf6a8d2d75b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_data_path(file)\n",
    "    joinpath(@__DIR__, \"../../../data/$file\")\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "73189f0a-b0f5-4f2d-b53a-dae4f74055ff",
   "metadata": {},
   "outputs": [],
   "source": [
    "function seed_rng!(salt::String)\n",
    "    init = first(read_csv(get_data_path(\"rng.csv\")).seed)\n",
    "    seed = first(reinterpret(UInt64, SHA.sha256(init * salt)))\n",
    "    Random.seed!(seed)\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "43d31668-7da2-44b3-b3ec-f0fbc2ea5751",
   "metadata": {},
   "outputs": [],
   "source": [
    "@memoize function get_items(medium::String, source::String)\n",
    "    itemcols = Dict(\n",
    "        \"mal\" => \"uid\",\n",
    "        \"anilist\" => \"anilistid\",\n",
    "        \"kitsu\" => \"kitsuid\",\n",
    "        \"animeplanet\" => \"url\",\n",
    "    )\n",
    "    files = Glob.glob(get_data_path(\"/$source/user_media_facts/user_$(medium)_list.*.csv\"))\n",
    "    items = Dict{Tuple{String,String},Int64}()\n",
    "    @showprogress for f in files\n",
    "        df = read_csv(f)\n",
    "        pop = StatsBase.countmap(df[:, itemcols[source]])\n",
    "        for (k, v) in pop\n",
    "            k = (source, k)\n",
    "            if k ∉ keys(items)\n",
    "                items[k] = 0\n",
    "            end\n",
    "            items[k] += v\n",
    "        end\n",
    "    end\n",
    "\n",
    "    validids = Set(\n",
    "        (source, x) for\n",
    "        x in read_csv(get_data_path(\"media/sources/$source.$medium.csv\"); ntasks = 1).uid\n",
    "    )\n",
    "    for x in validids\n",
    "        if x ∉ keys(items)\n",
    "            items[x] = 0\n",
    "        end\n",
    "    end\n",
    "    validitems = Dict{Tuple{String,String},Int64}()\n",
    "    for (k, v) in items\n",
    "        if k in validids\n",
    "            validitems[k] = v\n",
    "        else\n",
    "            if v >= 100\n",
    "                @info \"$source $medium missing item $k with $v impressions\"\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    validitems\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0541e526-57ed-47e0-bac3-f3903b329c99",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_edges(matchtype::String, medium::String, source1::String, source2::String)\n",
    "    df = read_csv(get_data_path(\"media/$matchtype/$medium.$source1.$source2.csv\"))\n",
    "    key1 = source1\n",
    "    key2 = source2\n",
    "    if source1 == source2\n",
    "        key2 *= \"_1\"\n",
    "    end\n",
    "    edges::Set{Tuple{itemtype,itemtype}} = Set()\n",
    "    for (u, v) in zip(df[:, key1], df[:, key2])\n",
    "        push!(edges, ((source1, u), (source2, v)))\n",
    "    end\n",
    "    edges\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3228b8fa-7f71-4046-88c5-ab0987ae0f9d",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_edges(\n",
    "    matchtypes::Vector{String},\n",
    "    medium::String,\n",
    "    source1::String,\n",
    "    source2::String,\n",
    ")\n",
    "    edges::Dict{itemtype,itemtype} = Dict()\n",
    "    vertices_with_conflicts = Set{itemtype}()\n",
    "    invalid_edges = get_edges(\"manual/invalid\", medium, source1, source2)\n",
    "    seen_invalid_edges = Set()\n",
    "    for matchtype in matchtypes\n",
    "        for (u, v) in get_edges(matchtype, medium, source1, source2)\n",
    "            if (u, v) in invalid_edges\n",
    "                push!(seen_invalid_edges, (u, v))\n",
    "                continue\n",
    "            elseif u ∉ keys(edges)\n",
    "                edges[u] = v\n",
    "            elseif edges[u] != v\n",
    "                @warn \"Edge ($u, $(edges[u])) conflicts with edge ($u, $v)\"\n",
    "                push!(vertices_with_conflicts, u)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    for x in vertices_with_conflicts\n",
    "        delete!(edges, x)\n",
    "    end\n",
    "    for x in setdiff(invalid_edges, seen_invalid_edges)\n",
    "        @info \"stale invalid edge $x\"\n",
    "    end\n",
    "    seen_valid_edges = Set()\n",
    "    for (u, v) in get_edges(\"manual/valid\", medium, source1, source2)\n",
    "        if u ∈ keys(edges)\n",
    "            if edges[u] == v\n",
    "                @info \"stale valid edge $((u, v))\"\n",
    "            else\n",
    "                @warn \"Overwriting edge $((u, edges[u])) with valid edge $((u, v))\"\n",
    "                edges[u] = v\n",
    "            end\n",
    "        else\n",
    "            edges[u] = v\n",
    "        end\n",
    "    end\n",
    "    Set((u, v) for (u, v) in edges)\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7f391075-29c2-49e4-a4df-54279ca641dd",
   "metadata": {},
   "outputs": [],
   "source": [
    "function merge_items(medium::String, sources::Vector{String}, matchtypes::Vector{String})\n",
    "    edges::Set{Tuple{itemtype,itemtype}} = Set()\n",
    "    membership::Dict{itemtype,Vector{itemtype}} = Dict()\n",
    "    for i = 1:length(sources)\n",
    "        source1 = sources[i]\n",
    "        edge_dicts = [\n",
    "            Dict(k => v for (k, v) in get_edges(matchtypes, medium, source1, source2))\n",
    "            for source2 in sources[1:i-1]\n",
    "        ]\n",
    "        items_sorted_by_popularity =\n",
    "            sort(collect(get_items(medium, source1)), by = x -> x[2], rev = true)\n",
    "        @showprogress for (k, _) in items_sorted_by_popularity\n",
    "            matches = [ed[k] for ed in edge_dicts if k in keys(ed)]\n",
    "            if length(matches) > 0\n",
    "                # attempt to match to existing items\n",
    "                merge_candidates = []\n",
    "                edge_candidates = []\n",
    "                for x in matches\n",
    "                    if membership[x] ∉ merge_candidates\n",
    "                        push!(merge_candidates, membership[x])\n",
    "                        push!(edge_candidates, (k, x))\n",
    "                    end\n",
    "                end\n",
    "                merge_sources = [Set(s for (s, _) in mc) for mc in merge_candidates]\n",
    "                can_merge = sum(length.(merge_sources)) == length(union(merge_sources...))\n",
    "                if can_merge\n",
    "                    # merge all matching entries\n",
    "                    v = [x for mc in merge_candidates for x in mc]\n",
    "                    v = sort(v, by = k -> findfirst(x -> x == k[1], sources))\n",
    "                    push!(v, k)\n",
    "                    for x in v\n",
    "                        membership[x] = v\n",
    "                    end\n",
    "                    for e in edge_candidates\n",
    "                        push!(edges, e)\n",
    "                    end\n",
    "                else\n",
    "                    @warn \"conflicting matches $([(k, m) for m in matches]) | $(merge_candidates)\"\n",
    "                    membership[k] = [k]\n",
    "                end\n",
    "            else\n",
    "                membership[k] = [k]\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    Set(values(membership)), edges\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7cb06964-008f-4e7c-b9e7-cd9e83cc2ee6",
   "metadata": {},
   "outputs": [],
   "source": [
    "function merge_overlapping(\n",
    "    medium::String,\n",
    "    sources::Vector{String},\n",
    "    vertices::Set{Vector{itemtype}},\n",
    ")\n",
    "    membership::Dict{itemtype,Vector{itemtype}} =\n",
    "        Dict(item => v for v in vertices for item in v)\n",
    "    edges::Set{Tuple{itemtype,itemtype}} = Set()\n",
    "    for i = 1:length(sources)\n",
    "        source1 = sources[i]\n",
    "        for source2 in sources[1:i]\n",
    "            for matchtype in [\"manual/overlapping\", \"manual/merge\"]\n",
    "                for e in get_edges(matchtype, medium, source1, source2)\n",
    "                    v1 = get(membership, e[1], nothing)\n",
    "                    v2 = get(membership, e[2], nothing)\n",
    "                    if isnothing(v1) || isnothing(v2) || v1 == v2\n",
    "                        if source1 != source2\n",
    "                            @info \"stale overlapping edge $e\"\n",
    "                        end\n",
    "                    else\n",
    "                        v = vcat(v2, v1)\n",
    "                        v = sort(v, by = k -> findfirst(x -> x == k[1], sources))\n",
    "                        for x in v\n",
    "                            membership[x] = v\n",
    "                        end\n",
    "                        push!(edges, e)\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    Set(values(membership)), edges\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71986547-0066-4229-adb4-ccfd62188f0f",
   "metadata": {},
   "outputs": [],
   "source": [
    "@memoize function get_media(medium::String, source::String)\n",
    "    df = read_csv(get_data_path(\"media/sources/$source.$medium.csv\"); ntasks = 1)\n",
    "    Dict(df[i, \"uid\"] => df[i:i, :] for i = 1:DataFrames.nrow(df))\n",
    "end\n",
    "\n",
    "function get_entry(medium::String, sources::Vector{String}, items::Vector{itemtype})\n",
    "    source, uid = first(items)\n",
    "    df = get_media(medium, source)[uid]\n",
    "    df = DataFrames.select(df, DataFrames.Not([:uid]))\n",
    "    for s in sources\n",
    "        df[!, \"users_$(s)\"] = [0]\n",
    "    end\n",
    "    df[!, \"users\"] = [0]\n",
    "    for x in sources\n",
    "        df[!, x] = [[]]\n",
    "    end\n",
    "    for item in items\n",
    "        source, uid = item\n",
    "        first(df)[\"users_$(source)\"] =\n",
    "            max(first(df)[\"users_$(source)\"], get_items(medium, source)[item])\n",
    "        push!(first(df)[source], uid)\n",
    "    end\n",
    "    for x in sources\n",
    "        df[!, x] = JSON.json.(df[:, x])\n",
    "        df[:, \"users\"] += df[:, \"users_$(x)\"]\n",
    "    end\n",
    "    df\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "48b43aea-9863-4e90-abcf-ed9301b299d1",
   "metadata": {},
   "outputs": [],
   "source": [
    "function save_matches(medium::String)\n",
    "    outdir = get_data_path(\"/media/match\")\n",
    "    if !ispath(outdir)\n",
    "        mkpath(outdir)\n",
    "    end\n",
    "    sources = [\"mal\", \"anilist\", \"kitsu\", \"animeplanet\"]\n",
    "    v, merge_edges = merge_items(medium, sources, [\"malid\", \"manami\", \"metadata\"])\n",
    "    v, overlapping_edges = merge_overlapping(medium, sources, v)\n",
    "    e = union(merge_edges, overlapping_edges)\n",
    "\n",
    "    # save media\n",
    "    rows = [get_entry(medium, sources, x) for x in v]\n",
    "    media = sort(reduce(vcat, rows), :users, rev = true)\n",
    "    # filter out rare items\n",
    "    media = DataFrames.filter(x -> x.users >= 100, media)\n",
    "    # randomly assign uids\n",
    "    media = Random.shuffle(media)\n",
    "    media[!, :uid] = 1:DataFrames.nrow(media)\n",
    "    CSV.write(\"$outdir/$medium.csv\", media)\n",
    "\n",
    "    # save edges\n",
    "    function num_users(medium::String, edge::Tuple{itemtype,itemtype})\n",
    "        users(x) = get_items(medium, first(x))[x]\n",
    "        sum(users.(edge))\n",
    "    end\n",
    "    edges = DataFrames.DataFrame(\n",
    "        [tuple(Iterators.flatten(x)..., num_users(medium, x)) for x in e],\n",
    "        [:source1, :uid1, :source2, :uid2, :users],\n",
    "    )\n",
    "    edges = sort(edges, :users, rev = true)\n",
    "    CSV.write(\"$outdir/edges.$medium.csv\", edges)\n",
    "\n",
    "    # save unmerged items\n",
    "    unmerged_items = DataFrames.select(\n",
    "        filter(x -> isempty(JSON.parse(x[sources[1]])), media),\n",
    "        [sources[2:end]; [\"users\"]],\n",
    "    )\n",
    "    CSV.write(\"$outdir/unmerged.$medium.csv\", unmerged_items)\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b6a2b6fa-5323-4db1-a26a-4a31cba586ed",
   "metadata": {},
   "outputs": [],
   "source": [
    "seed_rng!(\"ImportDatasets/ImportMedia/Match\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3757e3b8-55f1-4a1a-ab69-d9fd050b58dc",
   "metadata": {},
   "outputs": [],
   "source": [
    "save_matches(\"manga\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d12559ff-0ad1-4613-8f7b-cc3344be1295",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "save_matches(\"anime\");"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.2",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
