{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "b4065547-bb08-427f-be9b-b57f90197f74",
   "metadata": {},
   "source": [
    "# Match items using media metadata"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d4050554-666c-4d54-838c-46ec64780e73",
   "metadata": {},
   "outputs": [],
   "source": [
    "import JupyterFormatter\n",
    "JupyterFormatter.enable_autoformat();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b5e1343e-04a6-4259-9ba2-b42c2ea9bb02",
   "metadata": {},
   "outputs": [],
   "source": [
    "import CSV\n",
    "import DataFrames\n",
    "import Dates\n",
    "import JSON\n",
    "import Memoize: @memoize\n",
    "import ProgressMeter: @showprogress\n",
    "import Random\n",
    "import StringDistances"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "abc5235e-7a3e-4ed0-92d5-394191d65541",
   "metadata": {},
   "outputs": [],
   "source": [
    "struct TitleType\n",
    "    title::String\n",
    "    alttitle::String\n",
    "end\n",
    "\n",
    "struct DateType\n",
    "    date::Dates.DateTime\n",
    "    resolution::Int64\n",
    "end\n",
    "\n",
    "struct LengthType\n",
    "    length::Int64\n",
    "    plus::Bool\n",
    "    given::Bool\n",
    "end\n",
    "\n",
    "struct DurationType\n",
    "    duration::Float64\n",
    "    given::Bool\n",
    "end\n",
    "\n",
    "struct Media\n",
    "    uid::Vector{String}\n",
    "    title::Vector{TitleType}\n",
    "    summary::Vector{String}\n",
    "    mediatype::Vector{String}\n",
    "    startdate::Vector{DateType}\n",
    "    enddate::Vector{DateType}\n",
    "    episodes::Vector{LengthType}\n",
    "    duration::Vector{DurationType}\n",
    "    chapters::Vector{LengthType}\n",
    "    volumes::Vector{LengthType}\n",
    "    status::Vector{String}\n",
    "    season::Vector{String}\n",
    "    studios::Vector{Set{String}}\n",
    "    genres::Vector{String}\n",
    "    accessed_at::Vector{String}\n",
    "end;\n",
    "\n",
    "@memoize function get_media(source::String, medium::String)\n",
    "    function parse_title(x::String, y::String)::TitleType\n",
    "        return TitleType(lowercase(x), lowercase(y))\n",
    "    end\n",
    "\n",
    "    function parse_date(x::String)::DateType\n",
    "        if isempty(x)\n",
    "            return DateType(Dates.DateTime(1), 0)\n",
    "        end\n",
    "        r = parse.(Int64, split(x, \"-\"))\n",
    "        while length(r) > 0\n",
    "            try\n",
    "                Dates.DateTime(r...)\n",
    "                break\n",
    "            catch e\n",
    "                r = r[1:end-1]\n",
    "            end\n",
    "        end\n",
    "        DateType(Dates.DateTime(r...), length(r))\n",
    "    end\n",
    "\n",
    "    function parse_episodes(x::String)::LengthType\n",
    "        if isempty(x)\n",
    "            return LengthType(0, false, false)\n",
    "        end\n",
    "        plus = false\n",
    "        if endswith(x, \"+\")\n",
    "            x = x[1:end-1]\n",
    "            plus = true\n",
    "        end\n",
    "        LengthType(parse(Int64, x), plus, true)\n",
    "    end\n",
    "\n",
    "    function parse_duration(x::String)::DurationType\n",
    "        if isempty(x)\n",
    "            return DurationType(0, false)\n",
    "        end\n",
    "        DurationType(parse(Float64, x), true)\n",
    "    end\n",
    "\n",
    "    parse_studios(x::String)::Set{String} = Set(lowercase(x) for x in JSON.parse(x))\n",
    "\n",
    "    df = CSV.read(\n",
    "        \"../../../data/media/sources/$source.$medium.csv\",\n",
    "        DataFrames.DataFrame;\n",
    "        ntasks = 1,\n",
    "        types = String,\n",
    "        missingstring = nothing,\n",
    "    )\n",
    "    Media(\n",
    "        df.uid,\n",
    "        parse_title.(df.title, df.alttitle),\n",
    "        df.summary,\n",
    "        df.mediatype,\n",
    "        parse_date.(df.startdate),\n",
    "        parse_date.(df.enddate),\n",
    "        parse_episodes.(df.episodes),\n",
    "        parse_duration.(df.duration),\n",
    "        parse_episodes.(df.chapters),\n",
    "        parse_episodes.(df.volumes),\n",
    "        df.status,\n",
    "        df.season,\n",
    "        parse_studios.(df.studios),\n",
    "        df.genres,\n",
    "        df.accessed_at,\n",
    "    )\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0c098315-d480-4d8c-a153-b52a195db469",
   "metadata": {},
   "outputs": [],
   "source": [
    "@memoize function get_mediatypes(medium)\n",
    "    if medium == \"manga\"\n",
    "        manga_types = Set([\"Manhwa\", \"Manhua\", \"Manga\", \"OEL\"])\n",
    "        novel_types = Set([\"Light Novel\", \"Novel\"])\n",
    "        shortmanga_types = Set([\"One-shot\", \"Doujinshi\"])\n",
    "        return (manga_types, novel_types, shortmanga_types)\n",
    "    elseif medium == \"anime\"\n",
    "        tv_types = Set([\"ONA\", \"TV\"])\n",
    "        shortanime_types = Set([\"Music\", \"CM\", \"PV\", \"Special\"])\n",
    "        special_types = Set([\"OVA\", \"Special\", \"TV Special\"])\n",
    "        movie_types = Set([\"Movie\"])\n",
    "        return (tv_types, shortanime_types, special_types, movie_types)\n",
    "    else\n",
    "        @assert false\n",
    "    end\n",
    "end\n",
    "\n",
    "function match_mediatype(medium::String, t1::String, t2::String, fuzzy::Bool)\n",
    "    if isempty(t1) || isempty(t2)\n",
    "        return 0\n",
    "    end\n",
    "    if fuzzy\n",
    "        for types in get_mediatypes(medium)\n",
    "            if t1 in types && t2 in types\n",
    "                return 1\n",
    "            end\n",
    "        end\n",
    "        return -1\n",
    "    else\n",
    "        return t1 == t2\n",
    "    end\n",
    "end\n",
    "\n",
    "function match_date(d1::DateType, d2::DateType, fuzzy::Bool)\n",
    "    if d1.resolution == 0 || d2.resolution == 0\n",
    "        return 0\n",
    "    end\n",
    "    if fuzzy\n",
    "        if abs(d1.date - d2.date) <= Dates.Day(31)\n",
    "            return 1\n",
    "        end\n",
    "    end\n",
    "    N = min(d1.resolution, d2.resolution)\n",
    "    fns = (Dates.year, Dates.month, Dates.day)\n",
    "    for i = 1:N\n",
    "        if fns[i](d1.date) != fns[i](d2.date)\n",
    "            return fuzzy ? -1 : 0\n",
    "        end\n",
    "    end\n",
    "    1\n",
    "end\n",
    "\n",
    "function match_season(s1::String, s2::String, fuzzy::Bool)\n",
    "    if isempty(s1) || isempty(s2)\n",
    "        return 0\n",
    "    end\n",
    "    if s1 == s2\n",
    "        return 1\n",
    "    else\n",
    "        return fuzzy ? -1 : 0\n",
    "    end\n",
    "end\n",
    "\n",
    "@memoize function get_statustypes()\n",
    "    released = Set([\"Finished\", \"Releasing\", \"Cancelled\", \"On Hiatus\"])\n",
    "    unreleased = Set([\"TBA\", \"Upcoming\"])\n",
    "    (released, unreleased)\n",
    "end\n",
    "\n",
    "function match_status(s1::String, s2::String, fuzzy::Bool)\n",
    "    if isempty(s1) || isempty(s2)\n",
    "        return 0\n",
    "    end\n",
    "    if fuzzy\n",
    "        # an item can transition from upcoming -> releasing\n",
    "        if Set((s1, s2)) == Set((\"Upcoming\", \"Releasing\"))\n",
    "            return 0\n",
    "        end\n",
    "        for types in get_statustypes()\n",
    "            if s1 in types && s2 in types\n",
    "                return 1\n",
    "            end\n",
    "        end\n",
    "        return -1\n",
    "    else\n",
    "        return s1 == s2\n",
    "    end\n",
    "end\n",
    "\n",
    "function match_episodes(e1::LengthType, e2::LengthType, fuzzy::Bool)\n",
    "    if !e1.given || !e2.given\n",
    "        return 0\n",
    "    end\n",
    "    if fuzzy\n",
    "        n1 = e1.length\n",
    "        n2 = e2.length\n",
    "        if abs(n1 - n2) <= 1\n",
    "            return 1\n",
    "        elseif min(n1 / n2, n2 / n1) >= 0.8\n",
    "            return 1\n",
    "        elseif e1.plus || e2.plus\n",
    "            return 0\n",
    "        else\n",
    "            return -1\n",
    "        end\n",
    "    else\n",
    "        return e1.length == e2.length\n",
    "    end\n",
    "end\n",
    "\n",
    "function match_duration(d1::DurationType, d2::DurationType, fuzzy::Bool)\n",
    "    if !d1.given || !d2.given\n",
    "        return 0\n",
    "    end\n",
    "    if fuzzy\n",
    "        n1 = d1.duration\n",
    "        n2 = d2.duration\n",
    "        if abs(n1 - n2) <= 3\n",
    "            return 1\n",
    "        elseif min(n1 / n2, n2 / n1) >= 0.8\n",
    "            return 1\n",
    "        else\n",
    "            return -1\n",
    "        end\n",
    "    else\n",
    "        return d1 == d2\n",
    "    end\n",
    "end\n",
    "\n",
    "function match_studios(s1::Set{String}, s2::Set{String}, fuzzy::Bool)\n",
    "    if length(s1) == 0 || length(s2) == 0\n",
    "        return 0\n",
    "    end\n",
    "    if fuzzy\n",
    "        return !isdisjoint(s1, s2)\n",
    "    else\n",
    "        return s1 == s2\n",
    "    end\n",
    "end\n",
    "\n",
    "function matchstring(x::String, y::String, fuzzy::Bool)\n",
    "    if isempty(x) || isempty(y)\n",
    "        return 0\n",
    "    end\n",
    "    if fuzzy\n",
    "        cutoff = 0.9\n",
    "        match = StringDistances.compare(x, y, StringDistances.Levenshtein()) > cutoff\n",
    "        return match ? 1 : -1\n",
    "    else\n",
    "        return x == y\n",
    "    end\n",
    "end\n",
    "\n",
    "# macro for the following operation\n",
    "# if expr == errcode\n",
    "#     return errcode\n",
    "# else\n",
    "#     accum += expr\n",
    "# end\n",
    "macro earlyreturn(errcode, accum, expr)\n",
    "    esc(quote\n",
    "        _r = $(expr)\n",
    "        if _r == $(errcode)\n",
    "            return $(errcode)\n",
    "        else\n",
    "            $(accum) += _r\n",
    "        end\n",
    "    end)\n",
    "end\n",
    "\n",
    "function match_titles(t1::TitleType, t2::TitleType, fuzzy::Bool)\n",
    "    n = 0\n",
    "    @earlyreturn 1 n matchstring(t1.title, t2.title, fuzzy)\n",
    "    @earlyreturn 1 n matchstring(t1.title, t2.alttitle, fuzzy)\n",
    "    @earlyreturn 1 n matchstring(t1.alttitle, t2.title, fuzzy)\n",
    "    @earlyreturn 1 n matchstring(t1.alttitle, t2.alttitle, fuzzy)\n",
    "    return fuzzy ? -1 : 0\n",
    "end\n",
    "\n",
    "function fuzzy(fn::Function, args...)\n",
    "    n = 0\n",
    "    @earlyreturn -1 n fn(args..., true)\n",
    "    @earlyreturn -1 n fn(args..., false)\n",
    "    n\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7c371966-17f3-42de-ac13-1ed7dc675b54",
   "metadata": {},
   "outputs": [],
   "source": [
    "function match_rows(medium, df1, i, df2, j)\n",
    "    n = 0\n",
    "    @earlyreturn -1 n fuzzy(match_mediatype, medium, df1.mediatype[i], df2.mediatype[j])\n",
    "    @earlyreturn -1 n fuzzy(match_date, df1.startdate[i], df2.startdate[j])\n",
    "    @earlyreturn -1 n fuzzy(match_date, df1.enddate[i], df2.enddate[j])\n",
    "    @earlyreturn -1 n fuzzy(match_season, df1.season[i], df2.season[j])\n",
    "    @earlyreturn -1 n fuzzy(match_status, df1.status[i], df2.status[j])\n",
    "    @earlyreturn -1 n fuzzy(match_episodes, df1.episodes[i], df2.episodes[j])\n",
    "    @earlyreturn -1 n fuzzy(match_episodes, df1.chapters[i], df2.chapters[j])\n",
    "    @earlyreturn -1 n fuzzy(match_episodes, df1.volumes[i], df2.volumes[j])\n",
    "    @earlyreturn -1 n fuzzy(match_duration, df1.duration[i], df2.duration[j])\n",
    "    @earlyreturn -1 n fuzzy(match_studios, df1.studios[i], df2.studios[j])\n",
    "    @earlyreturn -1 n fuzzy(match_titles, df1.title[i], df2.title[j])\n",
    "    n\n",
    "end\n",
    "\n",
    "function match_metadata(source1, source2, medium, idxs, showprogress)\n",
    "    media1 = get_media(source1, medium)\n",
    "    media2 = get_media(source2, medium)\n",
    "    matches = Dict{String,String}()\n",
    "    @showprogress enabled = showprogress for i in idxs\n",
    "        candidate = nothing\n",
    "        max_matches = 0\n",
    "        for j = 1:length(media2.uid)\n",
    "            nmatches = match_rows(medium, media1, i, media2, j)\n",
    "            if nmatches == max_matches\n",
    "                candidate = nothing\n",
    "            elseif nmatches > max_matches\n",
    "                candidate = j\n",
    "                max_matches = nmatches\n",
    "            end\n",
    "        end\n",
    "        if !isnothing(candidate)\n",
    "            matches[media1.uid[i]] = media2.uid[candidate]\n",
    "        end\n",
    "    end\n",
    "    matches\n",
    "end\n",
    "\n",
    "function match_metadata(source1, source2, medium)\n",
    "    nchunks = Threads.nthreads()\n",
    "    idxs = Random.shuffle(1:length(get_media(source1, medium).uid))\n",
    "    chunks = Iterators.partition(idxs, div(length(idxs), nchunks))\n",
    "    tasks = map(Iterators.enumerate(chunks)) do (i, chunk)\n",
    "        Threads.@spawn match_metadata(source1, source2, medium, chunk, i == 1)\n",
    "    end\n",
    "    matches = fetch.(tasks)\n",
    "    reduce(merge, matches)\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "abb6c9d4-39ec-46f0-a7c1-4929cfc3581e",
   "metadata": {},
   "outputs": [],
   "source": [
    "outdir = \"../../../data/media/metadata\"\n",
    "mkpath(outdir)\n",
    "sources = [\"mal\", \"anilist\", \"kitsu\", \"animeplanet\"]\n",
    "for medium in [\"manga\", \"anime\"]\n",
    "    for i = 1:length(sources)\n",
    "        for j = i+1:length(sources)\n",
    "            matches = match_metadata(sources[j], sources[i], medium)\n",
    "            open(\"$outdir/$medium.$(sources[j]).$(sources[i]).csv\", \"w\") do f\n",
    "                write(f, \"$(sources[j]),$(sources[i])\\n\")\n",
    "                for (k, v) in matches\n",
    "                    write(f, \"$k,$v\\n\")\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.2",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
