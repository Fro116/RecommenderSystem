{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1f4b06dc-ca15-4978-a2ab-ba3e26fd8f05",
   "metadata": {},
   "outputs": [],
   "source": [
    "import JupyterFormatter\n",
    "JupyterFormatter.enable_autoformat();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a961f9b7-613c-4ed1-9895-f7fd26199b58",
   "metadata": {},
   "outputs": [],
   "source": [
    "const DB_CONNECTION_STRING = read(\"../../../environment/database/primary.txt\", String);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2e41a500-59b7-4185-b848-14955cb23ebf",
   "metadata": {},
   "outputs": [],
   "source": [
    "import Dates\n",
    "import LibPQ\n",
    "import SHA\n",
    "import JSON3\n",
    "\n",
    "const STDOUT_LOCK = ReentrantLock()\n",
    "\n",
    "function logerror(x::String)\n",
    "    Threads.lock(STDOUT_LOCK) do\n",
    "        println(\"$(Dates.now()) [ERROR] $x\")\n",
    "        println(catch_backtrace())\n",
    "    end\n",
    "end\n",
    "\n",
    "const CONNECTION = LibPQ.Connection(DB_CONNECTION_STRING)\n",
    "const CONNECTION_LOCK = ReentrantLock()\n",
    "\n",
    "function db_update_junction_table(\n",
    "    primary_table::String,\n",
    "    junction_table::String,\n",
    "    idcols::Vector{String},\n",
    "    idvals::Vector,\n",
    "    primary_data::Union{Dict,Nothing},\n",
    "    junction_data::Union{Vector,Nothing},\n",
    "    success::Bool,\n",
    ")\n",
    "    lock(CONNECTION_LOCK) do\n",
    "        try\n",
    "            db_update_junction_table(\n",
    "                CONNECTION,\n",
    "                primary_table,\n",
    "                junction_table,\n",
    "                idcols,\n",
    "                idvals,\n",
    "                primary_data,\n",
    "                junction_data,\n",
    "                success,\n",
    "            )\n",
    "        catch\n",
    "            logerror(\"db_update_junction_table connection error $idcols $idvals\")\n",
    "            LibPQ.reset!(CONNECTION; throw_error = false)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function db_update_primary_table(\n",
    "    table::String,\n",
    "    idcol::String,\n",
    "    idval::Union{String,Int},\n",
    "    data::Dict,\n",
    "    success::Bool,\n",
    ")\n",
    "    lock(CONNECTION_LOCK) do\n",
    "        try\n",
    "            db_update_primary_table(CONNECTION, table, idcol, idval, data, success)\n",
    "        catch\n",
    "            logerror(\"db_update_primary_table connection error $idcol $idval\")\n",
    "            LibPQ.reset!(CONNECTION; throw_error = false)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function canonical_hash(d::AbstractDict)\n",
    "    canon(x::AbstractDict) = Dict(k => canon(x[k]) for k in sort(collect(keys(x))))\n",
    "    canon(x::AbstractVector) = [canon(y) for y in x]\n",
    "    canon(x) = x\n",
    "    bytes2hex(SHA.sha256(JSON3.write(canon(d))))\n",
    "end\n",
    "function canonical_hash(d::AbstractVector)\n",
    "    hashes = sort([canonical_hash(x) for x in d])\n",
    "    bytes2hex(SHA.sha256(JSON3.write(hashes)))\n",
    "end\n",
    "function normalize(d::AbstractDict)\n",
    "    norm(x::AbstractDict) = JSON3.write(x)\n",
    "    norm(x::AbstractVector) = JSON3.write(x)\n",
    "    norm(::Nothing) = missing\n",
    "    norm(x) = x\n",
    "    Dict(k => norm(v) for (k, v) in d)\n",
    "end\n",
    "normalize(d::AbstractVector) = [normalize(x) for x in d]\n",
    "\n",
    "function db_upsert(\n",
    "    conn::LibPQ.Connection,\n",
    "    table::String,\n",
    "    idcols::Vector{String},\n",
    "    data::Dict,\n",
    "    conflict_rules::Dict = Dict(),\n",
    ")\n",
    "    data = normalize(data)\n",
    "    cols = collect(keys(data))\n",
    "    vals = Tuple(data[k] for k in cols)\n",
    "    col_str = join(cols, \", \")\n",
    "    idcol_str = join(idcols, \", \")\n",
    "    placeholders = join([\"\\$\" * string(i) for i = 1:length(vals)], \", \")\n",
    "    conflict_rules =\n",
    "        merge(Dict(c => c * \" = EXCLUDED.\" * c for c in cols if c âˆ‰ idcols), conflict_rules)\n",
    "    update_assignments = join(collect(values(conflict_rules)), \", \")\n",
    "    query = \"\"\"\n",
    "    INSERT INTO $table ($col_str) VALUES ($placeholders)\n",
    "    ON CONFLICT ($idcol_str) DO UPDATE SET $update_assignments;\n",
    "    \"\"\"\n",
    "    LibPQ.execute(conn, query, vals)\n",
    "end;\n",
    "\n",
    "function db_update_primary_table(\n",
    "    conn::LibPQ.Connection,\n",
    "    table::String,\n",
    "    idcol::String,\n",
    "    idval::Union{String,Int},\n",
    "    data::Dict,\n",
    "    success::Bool,\n",
    ")\n",
    "    curtime = time()\n",
    "    if !success\n",
    "        failure_data = Dict(\n",
    "            idcol => idval,\n",
    "            \"db_refreshed_at\" => curtime,\n",
    "            \"db_consecutive_failures\" => 1,\n",
    "        )\n",
    "        db_upsert(\n",
    "            conn,\n",
    "            table,\n",
    "            [idcol],\n",
    "            failure_data,\n",
    "            Dict(\n",
    "                \"db_consecutive_failures\" => \"db_consecutive_failures = $table.db_consecutive_failures + 1\",\n",
    "            ),\n",
    "        )\n",
    "        return\n",
    "    end\n",
    "    hash = canonical_hash(data)\n",
    "    let\n",
    "        col_str = join([idcol, \"db_entry_hash\"], \", \")\n",
    "        refresh = LibPQ.execute(\n",
    "            conn,\n",
    "            \"\"\"\n",
    "            UPDATE $table\n",
    "            SET db_refreshed_at = \\$1,\n",
    "            db_last_success_at = \\$1,\n",
    "            db_consecutive_failures = 0\n",
    "            WHERE ($col_str) = (\\$2, \\$3);\n",
    "            \"\"\",\n",
    "            (curtime, idval, hash),\n",
    "        )\n",
    "        if LibPQ.num_affected_rows(refresh) > 0\n",
    "            return\n",
    "        end\n",
    "    end\n",
    "    db_metadata = Dict(\n",
    "        \"db_refreshed_at\" => curtime,\n",
    "        \"db_last_changed_at\" => curtime,\n",
    "        \"db_entry_hash\" => hash,\n",
    "        \"db_last_success_at\" => curtime,\n",
    "        \"db_consecutive_failures\" => 0,\n",
    "    )\n",
    "    db_upsert(conn, table, [idcol], merge(data, db_metadata))\n",
    "end\n",
    "\n",
    "function db_update_junction_table(\n",
    "    conn::LibPQ.Connection,\n",
    "    primary_table::String,\n",
    "    junction_table::String,\n",
    "    idcols::Vector{String},\n",
    "    idvals::Vector,\n",
    "    primary_data::Union{Dict,Nothing},\n",
    "    junction_data::Union{Vector,Nothing},\n",
    "    success::Bool,\n",
    ")\n",
    "    curtime = time()\n",
    "    if !success\n",
    "        failure_data = Dict(\n",
    "            (idcols .=> idvals)...,\n",
    "            \"db_refreshed_at\" => curtime,\n",
    "            \"db_consecutive_failures\" => 1,\n",
    "        )\n",
    "        db_upsert(\n",
    "            conn,\n",
    "            primary_table,\n",
    "            idcols,\n",
    "            failure_data,\n",
    "            Dict(\n",
    "                \"db_consecutive_failures\" => \"db_consecutive_failures = $primary_table.db_consecutive_failures + 1\",\n",
    "            ),\n",
    "        )\n",
    "        return\n",
    "    end\n",
    "    primary_hash = canonical_hash(primary_data)\n",
    "    if isnothing(junction_data)\n",
    "        let\n",
    "            col_list = [idcols..., \"db_primary_hash\"]\n",
    "            col_str = join(col_list, \", \")\n",
    "            placeholders = join([\"\\$\" * string(i + 1) for i = 1:length(col_list)], \", \")\n",
    "            refresh = LibPQ.execute(\n",
    "                conn,\n",
    "                \"\"\"\n",
    "                UPDATE $primary_table\n",
    "                SET db_refreshed_at = \\$1,\n",
    "                db_last_success_at = \\$1,\n",
    "                db_consecutive_failures = 0\n",
    "                WHERE ($col_str) = ($placeholders);\n",
    "                \"\"\",\n",
    "                (curtime, idvals..., primary_hash),\n",
    "            )\n",
    "            if LibPQ.num_affected_rows(refresh) > 0\n",
    "                return\n",
    "            end\n",
    "        end\n",
    "        db_metadata = Dict(\n",
    "            \"db_refreshed_at\" => curtime,\n",
    "            \"db_primary_last_changed_at\" => curtime,\n",
    "            \"db_primary_hash\" => primary_hash,\n",
    "            \"db_last_success_at\" => curtime,\n",
    "            \"db_consecutive_failures\" => 0,\n",
    "        )\n",
    "        db_upsert(conn, primary_table, idcols, merge(primary_data, db_metadata))\n",
    "        return\n",
    "    end\n",
    "    junction_hash = canonical_hash(junction_data)\n",
    "    junction_data = normalize(junction_data)\n",
    "    let\n",
    "        col_list = [idcols..., \"db_primary_hash\", \"db_junction_hash\"]\n",
    "        col_str = join(col_list, \", \")\n",
    "        placeholders = join([\"\\$\" * string(i + 1) for i = 1:length(col_list)], \", \")\n",
    "        refresh = LibPQ.execute(\n",
    "            conn,\n",
    "            \"\"\"\n",
    "            UPDATE $primary_table\n",
    "            SET db_refreshed_at = \\$1,\n",
    "            db_last_success_at = \\$1,\n",
    "            db_consecutive_failures = 0\n",
    "            WHERE ($col_str) = ($placeholders);\n",
    "            \"\"\",\n",
    "            (curtime, idvals..., primary_hash, junction_hash),\n",
    "        )\n",
    "        if LibPQ.num_affected_rows(refresh) > 0\n",
    "            return\n",
    "        end\n",
    "    end\n",
    "    LibPQ.execute(conn, \"BEGIN;\")\n",
    "    try\n",
    "        db_metadata = Dict(\n",
    "            \"db_refreshed_at\" => curtime,\n",
    "            \"db_primary_last_changed_at\" => curtime,\n",
    "            \"db_primary_hash\" => primary_hash,\n",
    "            \"db_junction_last_changed_at\" => curtime,\n",
    "            \"db_junction_hash\" => junction_hash,\n",
    "            \"db_last_success_at\" => curtime,\n",
    "            \"db_consecutive_failures\" => 0,\n",
    "        )\n",
    "        db_upsert(conn, primary_table, idcols, merge(primary_data, db_metadata))\n",
    "        col_str = join(idcols, \", \")\n",
    "        placeholders = join([\"\\$\" * string(i) for i = 1:length(idcols)], \", \")\n",
    "        LibPQ.execute(\n",
    "            conn,\n",
    "            \"DELETE FROM $junction_table WHERE ($col_str) = ($placeholders);\",\n",
    "            idvals,\n",
    "        )\n",
    "        col_list = collect(keys(first(junction_data)))\n",
    "        col_str = join(col_list, \", \")\n",
    "        placeholders = join([\"\\$\" * string(i) for i = 1:length(col_list)], \", \")\n",
    "        LibPQ.load!(\n",
    "            (; (Symbol(k) => [x[k] for x in junction_data] for k in col_list)...),\n",
    "            conn,\n",
    "            \"INSERT INTO $junction_table ($col_str) VALUES ($placeholders);\",\n",
    "        )\n",
    "    finally\n",
    "        LibPQ.execute(conn, \"END\")\n",
    "    end\n",
    "end;"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.2",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
