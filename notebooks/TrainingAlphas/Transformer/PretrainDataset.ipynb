{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "5e701df7-0d06-4631-a382-8228a4d3a845",
   "metadata": {},
   "source": [
    "# Pretrain Dataset\n",
    "* Saves training data to disk"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d90afc10-1ae5-4665-937e-d58a5859ac94",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "import NBInclude: @nbinclude\n",
    "@nbinclude(\"../Alpha.ipynb\")\n",
    "@nbinclude(\"Data.ipynb\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bfd56259-4513-4297-a978-99c765532231",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "name = \"all/Transformer/v1\"\n",
    "set_logging_outdir(name);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "567d60ae-6b55-4274-85b1-c0f9c9fc7545",
   "metadata": {},
   "outputs": [],
   "source": [
    "import HDF5\n",
    "import JSON\n",
    "import MLUtils\n",
    "import Random\n",
    "import StatsBase: mean, sample"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c6434e07-f565-474a-ac05-12c409afe141",
   "metadata": {},
   "source": [
    "# Featurization"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c2ed0909-a7b0-4b54-ae7b-20dd5b9b789b",
   "metadata": {},
   "outputs": [],
   "source": [
    "featurize(sentence::Vector{wordtype}, config, rng) = featurize(;\n",
    "    sentence = sentence,\n",
    "    max_seq_len = config[:max_sequence_length],\n",
    "    vocab_sizes = config[:base_vocab_sizes],\n",
    "    pad_tokens = config[:pad_tokens],\n",
    "    cls_tokens = config[:cls_tokens],\n",
    "    mask_tokens = config[:mask_tokens],\n",
    "    rng = rng,\n",
    ");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9b524c73-58a0-4c31-9ff0-48f81eb0fa0f",
   "metadata": {},
   "outputs": [],
   "source": [
    "function featurize(;\n",
    "    sentence::Vector{wordtype},\n",
    "    max_seq_len,\n",
    "    vocab_sizes,\n",
    "    pad_tokens,\n",
    "    cls_tokens,\n",
    "    mask_tokens,\n",
    "    rng,\n",
    ")\n",
    "    sentence = subset_sentence(sentence, max_seq_len; recent = false, rng = rng)\n",
    "    tokens = get_token_ids(sentence, max_seq_len, pad_tokens, cls_tokens)\n",
    "\n",
    "    positions = Dict(\n",
    "        x => Dict(y => zeros(Int32, max_seq_len) for y in ALL_METRICS) for x in ALL_MEDIUMS\n",
    "    )\n",
    "    weights = Dict(\n",
    "        x => Dict(y => zeros(Float32, max_seq_len) for y in ALL_METRICS) for\n",
    "        x in ALL_MEDIUMS\n",
    "    )\n",
    "    labels = Dict(\n",
    "        x => Dict(y => zeros(Float32, max_seq_len) for y in ALL_METRICS) for\n",
    "        x in ALL_MEDIUMS\n",
    "    )\n",
    "    userids = Dict(\n",
    "        x => Dict(y => zeros(Int32, max_seq_len) for y in ALL_METRICS) for x in ALL_MEDIUMS\n",
    "    )\n",
    "    for i::Int32 = 1:max_seq_len\n",
    "        is_manga = (extract(tokens, :itemid)[i] < num_items(\"manga\"))\n",
    "        is_anime =\n",
    "            num_items(\"manga\") <=\n",
    "            extract(tokens, :itemid)[i] <\n",
    "            num_items(\"manga\") + num_items(\"anime\")\n",
    "        has_rating =\n",
    "            0 < extract(tokens, :rating)[i] <= vocab_sizes[get_wordtype_index(:rating)]\n",
    "        has_watch =\n",
    "            get_status(:plan_to_watch) <\n",
    "            extract(tokens, :status)[i] <=\n",
    "            vocab_sizes[get_wordtype_index(:status)]\n",
    "        has_plantowatch = extract(tokens, :status)[i] == get_status(:plan_to_watch)\n",
    "        has_drop =\n",
    "            get_status(:none) <\n",
    "            extract(tokens, :status)[i] <=\n",
    "            vocab_sizes[get_wordtype_index(:status)]\n",
    "        if !(has_rating || has_watch || has_plantowatch || has_drop)\n",
    "            continue\n",
    "        end\n",
    "        @assert xor(is_manga, is_anime)\n",
    "        if is_manga\n",
    "            medium = \"manga\"\n",
    "            mediaid = extract(tokens, :itemid)[i]\n",
    "        elseif is_anime\n",
    "            medium = \"anime\"\n",
    "            mediaid = extract(tokens, :itemid)[i] - num_items(\"manga\")\n",
    "        else\n",
    "            @assert false\n",
    "        end\n",
    "\n",
    "        # mask and make predictions for 15% of tokens\n",
    "        if rand(rng) > 0.15\n",
    "            continue\n",
    "        end\n",
    "        if has_rating\n",
    "            positions[medium][\"rating\"][i] = mediaid\n",
    "            labels[medium][\"rating\"][i] = extract(tokens, :rating)[i]\n",
    "            weights[medium][\"rating\"][i] = 1\n",
    "            userids[medium][\"rating\"][i] = extract(tokens, :userid)[i]\n",
    "        end\n",
    "        if has_watch\n",
    "            positions[medium][\"watch\"][i] = mediaid\n",
    "            labels[medium][\"watch\"][i] = 1\n",
    "            weights[medium][\"watch\"][i] = 1\n",
    "            userids[medium][\"watch\"][i] = extract(tokens, :userid)[i]\n",
    "        end\n",
    "        if has_plantowatch\n",
    "            positions[medium][\"plantowatch\"][i] = mediaid\n",
    "            labels[medium][\"plantowatch\"][i] = 1\n",
    "            weights[medium][\"plantowatch\"][i] = 1\n",
    "            userids[medium][\"plantowatch\"][i] = extract(tokens, :userid)[i]\n",
    "        end\n",
    "        if has_drop\n",
    "            positions[medium][\"drop\"][i] = mediaid\n",
    "            labels[medium][\"drop\"][i] = extract(tokens, :status)[i] <= get_status(:dropped)\n",
    "            weights[medium][\"drop\"][i] = 1\n",
    "            userids[medium][\"drop\"][i] = extract(tokens, :userid)[i]\n",
    "        end\n",
    "\n",
    "        # do bert masking        \n",
    "        r = rand(rng)\n",
    "        if r <= 0.8\n",
    "            for j = 1:length(tokens)\n",
    "                if j ∉ get_wordtype_index.([:userid, :position])\n",
    "                    tokens[j][i] = mask_tokens[j]\n",
    "                end\n",
    "            end\n",
    "        elseif r <= 0.9\n",
    "            for j = 1:length(tokens)\n",
    "                if j ∉ get_wordtype_index.([:userid, :position])\n",
    "                    if eltype(vocab_sizes[j]) == Int32\n",
    "                        tokens[j][i] = rand(rng, 0:vocab_sizes[j]-1)\n",
    "                    elseif eltype(tokens[j]) == Float32\n",
    "                        tokens[j][i] = rand(rng) * vocab_sizes[j]\n",
    "                    else\n",
    "                        @assert false\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "        else\n",
    "            nothing\n",
    "        end\n",
    "    end\n",
    "\n",
    "    tokens, positions, labels, weights\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cae7ebe9-122d-4b77-a00b-87206686e4b3",
   "metadata": {},
   "source": [
    "# Data collection"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9d0deae8-f55a-4293-8ebb-84011b487f92",
   "metadata": {},
   "outputs": [],
   "source": [
    "function shuffle_training_data(rng, sentences, max_sequence_length, max_document_length)\n",
    "    order = Random.shuffle(rng, 1:length(sentences))\n",
    "    S = eltype(sentences)\n",
    "    W = eltype(sentences[1])\n",
    "\n",
    "    # patition tokens into minibatches\n",
    "    batched_sentences = Vector{S}()\n",
    "    sentence = Vector{W}()\n",
    "    for i in order\n",
    "        subset =\n",
    "            subset_sentence(sentences[i], max_document_length; recent = false, rng = rng)\n",
    "        for token in subset\n",
    "            push!(sentence, token)\n",
    "            if length(sentence) == max_sequence_length\n",
    "                push!(batched_sentences, sentence)\n",
    "                sentence = Vector{W}()\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    if length(sentence) > 0\n",
    "        push!(batched_sentences, sentence)\n",
    "    end\n",
    "    batched_sentences\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "90ed260a-b454-4690-ab8f-15ba4d686b6b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_sentences(rng, cls_tokens)\n",
    "    sentences = collect(values(get_training_data(cls_tokens)))\n",
    "    Random.shuffle!(rng, sentences)\n",
    "    cutoff = Int(round(0.99 * length(sentences))) # TODO switch to 0.999 for prod\n",
    "    training = sentences[1:cutoff]\n",
    "    validation = sentences[cutoff+1:end]\n",
    "    training, validation\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "50183c75-f670-425e-b08e-c72efe4e8988",
   "metadata": {},
   "source": [
    "# Configuration"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "805ac800-38d2-4ed9-9963-05c176baef7b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function set_rngs(seed)\n",
    "    rng = Random.Xoshiro(seed)\n",
    "    Random.seed!(rand(rng, UInt64))\n",
    "    rng\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "affb67b6-dabb-4db8-841d-c7ebf43f4a1b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function create_training_config()\n",
    "    # itemid, rating, updated_at, status, position, userid\n",
    "    max_sequence_length = 1024\n",
    "    base_vocab_sizes = (\n",
    "        Int32(sum(num_items.(ALL_MEDIUMS)) - 1),\n",
    "        Float32(10),\n",
    "        Float32(1),\n",
    "        Int32(get_status(:rewatching)),\n",
    "        Int32(max_sequence_length - 1),\n",
    "        Int32(num_users() - 1),\n",
    "    )\n",
    "    d = (\n",
    "        # tokenization\n",
    "        base_vocab_sizes = base_vocab_sizes,\n",
    "        cls_tokens = base_vocab_sizes .+ Int32(1),\n",
    "        pad_tokens = base_vocab_sizes .+ Int32(2),\n",
    "        mask_tokens = base_vocab_sizes .+ Int32(3),\n",
    "        vocab_sizes = base_vocab_sizes .+ Int32(4),\n",
    "        vocab_types = (\"int\", \"float\", \"float\", \"int\", \"int\", \"none\"),\n",
    "        media_sizes = Dict(m => num_items(m) for m in ALL_MEDIUMS),\n",
    "        # data\n",
    "        max_document_length = Inf, # TODO experiment with subsampling\n",
    "        chunk_size = 2^14,\n",
    "        training_epoch_size = -1,\n",
    "        validation_epoch_size = -1,\n",
    "        # model\n",
    "        max_sequence_length = max_sequence_length,\n",
    "        mode = \"pretrain\",\n",
    "    )\n",
    "    @assert d[:max_document_length] >= d[:max_sequence_length]\n",
    "    @assert length(d[:vocab_sizes]) == length(d[:vocab_types])\n",
    "    d\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8e9e8899-6268-4081-b7d1-d2b09a4b63e1",
   "metadata": {},
   "outputs": [],
   "source": [
    "function set_epoch_size(config, training_sentences, validation_sentences)\n",
    "    for (s, split) in\n",
    "        zip([training_sentences, validation_sentences], [\"training\", \"validation\"])\n",
    "        num_tokens = Int64(sum(min.(length.(s), config[:max_document_length])))\n",
    "        @info \"Number of $split tokens: $(num_tokens)\"\n",
    "        @info \"Number of $split sentences: $(length(s))\"\n",
    "        config = @set config[Symbol(\"$(split)_epoch_size\")] =\n",
    "            div(num_tokens, config[:max_sequence_length], RoundUp)\n",
    "    end\n",
    "    config\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bc31c5b2-42e2-455a-bd3f-0ca4eab8cb8f",
   "metadata": {},
   "outputs": [],
   "source": [
    "function setup_training(config, outdir)\n",
    "    mkpath(outdir)\n",
    "    fn = joinpath(outdir, \"config.json\")\n",
    "    open(fn, \"w\") do f\n",
    "        write(f, JSON.json(config))\n",
    "    end\n",
    "    for split in [\"training\", \"validation\"]\n",
    "        fn = joinpath(outdir, split)\n",
    "        mkpath(fn)\n",
    "        for x in readdir(fn, join = true)\n",
    "            rm(x, recursive = true)\n",
    "        end\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "106fa056-8deb-486d-9854-6e0991bc4714",
   "metadata": {},
   "source": [
    "# Disk I/O"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3c7abb52-f79b-4e89-8611-aa1506d2808e",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "function save_features(sentences, config, rng, filename)\n",
    "    sentences = shuffle_training_data(\n",
    "        rng,\n",
    "        sentences,\n",
    "        config[:max_sequence_length],\n",
    "        config[:max_document_length],\n",
    "    )\n",
    "    features = []\n",
    "    for x in sentences\n",
    "        push!(features, featurize(x, config, rng))\n",
    "    end\n",
    "\n",
    "    d = Dict{String,AbstractArray}()\n",
    "    collate = MLUtils.batch\n",
    "    embed_names = [\"itemid\", \"rating\", \"updated_at\", \"status\", \"position\", \"userid\"]\n",
    "    for (i, name) in Iterators.enumerate(embed_names)\n",
    "        d[name] = collate([x[1][i] for x in features])\n",
    "    end\n",
    "    for medium in ALL_MEDIUMS\n",
    "        for metric in ALL_METRICS\n",
    "            stem = \"$(medium)_$(metric)\"\n",
    "            d[\"positions_$stem\"] = collate([x[2][medium][metric] for x in features])\n",
    "            d[\"labels_$stem\"] = collate([x[3][medium][metric] for x in features])\n",
    "            d[\"weights_$stem\"] = collate([x[4][medium][metric] for x in features])\n",
    "        end\n",
    "    end\n",
    "    HDF5.h5open(filename, \"w\") do f\n",
    "        for (k, v) in d\n",
    "            f[k, deflate = 6] = v\n",
    "        end\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bcb8c46b-3c71-4e4b-a937-d57c355dd017",
   "metadata": {},
   "outputs": [],
   "source": [
    "function save_epoch(sentences, config, epoch, outdir)\n",
    "    workers = 16\n",
    "    outdir = joinpath(outdir, \"$epoch\")\n",
    "    mkpath(outdir)\n",
    "    rng = Random.Xoshiro(epoch)\n",
    "    Random.shuffle!(rng, sentences)\n",
    "    batches = collect(Iterators.partition(sentences, config[:chunk_size]))\n",
    "    seeds = [rand(rng, UInt64) for _ = 1:length(batches)]\n",
    "    @showprogress for idxs in Iterators.partition(1:length(batches), workers)\n",
    "        @sync for i in idxs\n",
    "            Threads.@spawn save_features(\n",
    "                batches[i],\n",
    "                config,\n",
    "                Random.Xoshiro(seeds[i]),\n",
    "                joinpath(outdir, \"$(i-1).h5\"),\n",
    "            )\n",
    "        end\n",
    "        GC.gc()\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "561138f9-5849-4640-a1d9-52175da8a573",
   "metadata": {},
   "outputs": [],
   "source": [
    "function save_epochs(num_epochs)\n",
    "    rng = set_rngs(20221221)\n",
    "    config = create_training_config()\n",
    "    @info \"loading data\"\n",
    "    training_sentences, validation_sentences = get_sentences(rng, config[:cls_tokens])\n",
    "    config = set_epoch_size(config, training_sentences, validation_sentences)\n",
    "    outdir = get_data_path(joinpath(\"alphas\", name))\n",
    "    setup_training(config, outdir)\n",
    "    for epoch = 0:num_epochs-1\n",
    "        @time save_epoch(\n",
    "            validation_sentences,\n",
    "            config,\n",
    "            epoch,\n",
    "            joinpath(outdir, \"validation\"),\n",
    "        )\n",
    "        @time save_epoch(training_sentences, config, epoch, joinpath(outdir, \"training\"))\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b0e388d5-7154-4cc9-a462-fa551d187eeb",
   "metadata": {},
   "outputs": [],
   "source": [
    "save_epochs(64)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.4",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
