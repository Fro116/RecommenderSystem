{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0239b7cb-85d8-497a-add1-6ff6b96148ac",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Taken from Transformers v0.1.25"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2d995632-e47c-4b5f-b264-5f5c00a55792",
   "metadata": {},
   "outputs": [],
   "source": [
    "import JupyterFormatter\n",
    "JupyterFormatter.enable_autoformat();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cdb73de3-a3bd-4b8a-b672-d71c79bd55cf",
   "metadata": {},
   "outputs": [],
   "source": [
    "import Functors: @functor"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e8d1a440-95e1-4c36-a1d0-5636f7b975d5",
   "metadata": {},
   "outputs": [],
   "source": [
    "abstract type AbstractEmbed{F} end\n",
    "abstract type AbstractBroadcastEmbed{F} <: AbstractEmbed{F} end\n",
    "\n",
    "Base.eltype(::AbstractEmbed{F}) where {F} = F\n",
    "_getF(::Type{<:AbstractEmbed{F}}) where {F} = F\n",
    "\n",
    "struct CompositeEmbedding{F,T<:NamedTuple,T2<:NamedTuple,P} <: AbstractEmbed{F}\n",
    "    embeddings::T\n",
    "    aggregator::T2\n",
    "    postprocessor::P\n",
    "    CompositeEmbedding(es::NamedTuple, as::NamedTuple, post) =\n",
    "        new{_getF(eltype(es)),typeof(es),typeof(as),typeof(post)}(es, as, post)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    CompositeEmbedding(;postprocessor=identity, es...)\n",
    "\n",
    "composite several embedding into one embedding according the aggregate methods and apply `postprocessor` on it.\n",
    "\"\"\"\n",
    "function CompositeEmbedding(; postprocessor = identity, es...)\n",
    "    eas = values(es)\n",
    "    emb = map(x -> x isa Tuple ? x[1] : x, eas)\n",
    "    agg = map(x -> x isa Tuple ? x[2] : +, eas)\n",
    "    CompositeEmbedding(emb, agg, postprocessor)\n",
    "end\n",
    "\n",
    "@functor CompositeEmbedding\n",
    "\n",
    "function Base.show(io::IO, e::CompositeEmbedding)\n",
    "    names = keys(e.embeddings)\n",
    "    firstname = first(names)\n",
    "\n",
    "    print(io, \"CompositeEmbedding(\")\n",
    "    print(io, firstname)\n",
    "    print(io, \" = \")\n",
    "    print(io, e.embeddings[firstname])\n",
    "\n",
    "    for name ∈ Base.tail(names)\n",
    "        print(io, \", \")\n",
    "        print(io, name)\n",
    "        print(io, \" = \")\n",
    "        print(io, e.embeddings[name])\n",
    "    end\n",
    "\n",
    "    if e.postprocessor !== identity\n",
    "        print(io, \", postprocessor = \")\n",
    "        print(io, e.postprocessor)\n",
    "    end\n",
    "\n",
    "    print(io, \")\")\n",
    "end\n",
    "\n",
    "get_value(e::AbstractEmbed, name::Symbol, xs::NamedTuple) = e(xs[name])\n",
    "_repeatdims(bs, vs) = ntuple(i -> i > length(vs) ? bs[i] : 1, length(vs))\n",
    "\n",
    "@inline aggregate(\n",
    "    e::E,\n",
    "    f::F,\n",
    "    base::A,\n",
    "    value::A2,\n",
    ") where {F,T,A<:AbstractArray{T},A2<:AbstractArray{T},E<:AbstractEmbed{T}} = f(base, value)\n",
    "@inline aggregate(\n",
    "    e::Eb,\n",
    "    f::F,\n",
    "    base::A,\n",
    "    value::A2,\n",
    ") where {F,T,A<:AbstractArray{T},A2<:AbstractArray{T},Eb<:AbstractBroadcastEmbed{T}} =\n",
    "    f.(base, value)\n",
    "@inline aggregate(\n",
    "    e::Eb,\n",
    "    f::typeof(vcat),\n",
    "    base::A,\n",
    "    value::A2,\n",
    ") where {T,A<:AbstractArray{T},A2<:AbstractArray{T},Eb<:AbstractBroadcastEmbed{T}} =\n",
    "    vcat(base, repeat(value, _repeatdims(size(base), size(value))...))\n",
    "@inline aggregate(\n",
    "    e::Eb,\n",
    "    f::typeof(hcat),\n",
    "    base::A,\n",
    "    value::A2,\n",
    ") where {T,A<:AbstractArray{T},A2<:AbstractArray{T},Eb<:AbstractBroadcastEmbed{T}} =\n",
    "    hcat(base, repeat(value, _repeatdims(size(base), size(value))...))\n",
    "\n",
    "(ce::CompositeEmbedding)(; xs...) = ce(values(xs))\n",
    "function (ce::CompositeEmbedding{F,T1,T2,P})(xs::NamedTuple) where {F,T1,T2,P}\n",
    "    names = keys(ce.embeddings)\n",
    "    firstname = first(names)\n",
    "    init = get_value(ce.embeddings[firstname], firstname, xs)\n",
    "\n",
    "    for name ∈ Base.tail(names)\n",
    "        emb = ce.embeddings[name]\n",
    "        agg = ce.aggregator[name]\n",
    "        value = get_value(emb, name, xs)\n",
    "        init = aggregate(emb, agg, init, value)\n",
    "    end\n",
    "\n",
    "    if P === typeof(identity)\n",
    "        init\n",
    "    else\n",
    "        ce.postprocessor(init)\n",
    "    end\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.3",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
